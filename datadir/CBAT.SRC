//-----------------------------------------------------------------------
int ButtonEvent()
  int self
  float floorz


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  if(!(self.event & 8))
    if(self.event == BUTTON_LEFT)
      floorz = RoomWaterLevel()
      if(floorz < self.floorz)
        floorz = self.floorz
      if(floorz < 0.0)
        floorz = 0.0
      if(self.hoverz < (floorz+20.0))
        self.hoverz = self.z + 1.00
      return 0
    if(self.event == BUTTON_RIGHT)
      floorz = RoomWaterLevel()
      if(floorz < self.floorz)
        floorz = self.floorz
      if(self.hoverz > (floorz+1.5))
        self.hoverz = self.hoverz - 0.30
      else
        // Make able to scrape ground to grab keys & stuff...
        self.vz = self.vz - 0.02
      return 0


//-----------------------------------------------------------------------
int Event()
  int self, petrify, subtype, target, drain_amount, child
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  petrify = self.pttimer
  STANDARD.DamageSoundEvent("FILE:-RAT01.RAW", NULL, NULL)
  if(self.event == EVENT_PETRIFIED)
    self.hits = 0
    self.action = ACTION_KNOCK_OUT_BEGIN
    self.daction = ACTION_KNOCK_OUT
    self.flags = self.flags | CHAR_FALL_ON
    self.nctimer = 30
    self.vz = 0.60
  target = NULL
  if(subtype == 3 && self.event == EVENT_HIT_CHARACTER)
    target = FindByIndex(SystemGet(SYS_COLLISIONCHAR, 0, 0))
    if(target)
      drain_amount = target.hits
  STANDARD.BumpDamageEvent(DAMAGE_EDGE, (1+subtype), 1, FALSE, TRUE)
  if(subtype == 3 && self.event == EVENT_HIT_CHARACTER)
    if(target)
      drain_amount = drain_amount - target.hits
      if(drain_amount > 0)
        self.hits = self.hits + drain_amount
        if(self.hits > (self.hitsmax-self.hitstap))
          drain_amount = drain_amount - (self.hits - (self.hitsmax-self.hitstap))
          self.hits = (self.hitsmax-self.hitstap)
        // Spawn the heal number particle...
        if(drain_amount > 0)
          child = Spawn(PARTICLE, self.x, self.y, self.z + (self.height - 1), "FILE:PNUMBER.RUN") 
          if(child)
            child.number = drain_amount
  STANDARD.Event()
  STANDARD.LevelUpEvent(4, 0, 2, 4, 0)
  if(self.action == ACTION_KNOCK_OUT_BEGIN)
    self.flags = (self.flags & (65535-CHAR_HOVER_ON)) | CHAR_GRAVITY_ON
    self.pttimer = petrify
  else
    if(self.action == ACTION_STUN_BEGIN)
      self.action = ACTION_JUMP
      self.daction = ACTION_JUMP
  if(subtype == 3)
    self.hitstap = 0

//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype, color
  self = FindSelf()
  subtype = (self.eqcol01>>6)
  color = (self.eqcol01)&15


  ModelAssign(self+MODEL_BASE_FILE, "FILE:BATANIM.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:DORANGE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:YELLOW.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=BAT.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:=EYE1.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, BLUE)


  if(subtype == 0)
    // Giant Bat
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:BAT.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DBLUE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:MAGENTA.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=BAT.RGB")
    STANDARD.EyeSetup(self+MODEL_CHEST_TEX3, self.team)
    ModelAssign(self+MODEL_CHEST_COLOR, BLUE)
  if(subtype == 1)
    // Cave Bat
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:BAT.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DORANGE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:YELLOW.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=BAT.RGB")
    STANDARD.EyeSetup(self+MODEL_CHEST_TEX3, self.team)
    ModelAssign(self+MODEL_CHEST_COLOR, BLUE)
  if(subtype == 2)
    // Dungeon Bat
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:BAT.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DPURPLE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=BAT.RGB")
    STANDARD.EyeSetup(self+MODEL_CHEST_TEX3, self.team)
    ModelAssign(self+MODEL_CHEST_COLOR, BLACK)
  if(subtype == 3)
    // Vampire Bat
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:BAT.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DRED.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:RED.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=BAT.RGB")
    STANDARD.EyeSetup(self+MODEL_CHEST_TEX3, self.team)
    ModelAssign(self+MODEL_CHEST_COLOR, DARK_RED)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Giant Bat", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Cave Bat", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Dungeon Bat", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Vampire Bat", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  if(CDRAGON.AISwoopAndRise(30))
    return FALSE
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  self.boxsize = 1.7
  self.height = 3
  self.flags = CHAR_FUZZY_ON | CHAR_HOVER_ON | CHAR_FINISH_ACTION
  self.sflags = SCRIPT_NO_KNOCK_OUT | SCRIPT_BUMP_DAMAGE
  self.vflags = VIRTUE_FLAG_AFTER_WATER | VIRTUE_FLAG_EATS_TRIPE
  self.vspin = 700
  self.timer = 1
  self.aitimer = 1
  self.hoverz = self.floorz + 3.0
  self.action = ACTION_JUMP
  self.daction = ACTION_JUMP
  self.flyfric = 0.995


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 13, 0, ITEM_NONE, ITEM_MEAT)
  self.hitsmax = 2*difficulty+1
  self.hits = self.hitsmax
  self.dex = 2*difficulty+6
  self.str = 1*difficulty+4
  self.int = 1.5*difficulty+5
  self.defbash = subtype-1
  self.defedge = subtype-1


  ModelSetup()

//-----------------------------------------------------------------------
