#define AISTATE_RANGED_AIM   255
#define AISTATE_RANGED_FIRE  254

//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, dex, i
  float x, y, angle, vx, vy


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  event = self.event & 7
  if((self.event & 8))
    ButtonHandled()
    if(event == BUTTON_SPECIAL1)
      // End flight if jump button is released...
      self.flags = self.flags & (65535 - CHAR_HOVER_ON)
      self.flags = self.flags | CHAR_GRAVITY_ON
      self.vspin = 1024
      return TRUE
  else
    if(event == BUTTON_SPECIAL1 && self.eqspec1 == ITEM_NONE)
      if((self.action == ACTION_WALK || self.action == ACTION_STAND || self.daction == ACTION_SWIM) && (self.flags & (CHAR_IN_WATER | CHAR_ON_GROUND | CHAR_ON_PLATFORM)))
        dex = self.dex
        if(dex > 50)
          dex = 50
        self.action = ACTION_JUMP_BEGIN
        self.daction = ACTION_JUMP
        self.vz = 0.65 + (dex * 0.0025)
        self.flags = self.flags | CHAR_FALL_ON | CHAR_HOVER_ON
        self.flags = self.flags & (65535 - CHAR_GRAVITY_ON)
        self.hoverz = self.z + (self.vz*5.0)
        self.vspin = 512
        ButtonHandled()
      return TRUE
  STANDARD.ButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self, subtype

  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2

  STANDARD.DamageSoundEvent("FILE:-CARR01.RAW", "FILE:-CARR02.RAW", "FILE:-CARR03.RAW")
  if(self.event == EVENT_PETRIFIED)
    if(self.flags & CHAR_HOVER_ON)
      self.flags = self.flags & (65535-CHAR_HOVER_ON)
      self.flags = self.flags | CHAR_GRAVITY_ON
    return 0

  STANDARD.Event()
  if(subtype == 1)
    STANDARD.LevelUpEvent(2, 4, 1, 1, 2)
  else
    STANDARD.LevelUpEvent(4, 0, 3, 3, 0)


//-----------------------------------------------------------------------
int FrameEvent()
  int self, color, subtype
  float offx, offy, vx, vy, angle
  self = FindSelf()
  if(self.event & 1)
    if(self.action == ACTION_JUMP_BEGIN || self.action == ACTION_JUMP || self.action == ACTION_KNOCK_OUT_BEGIN)
      // Lose some feathers...
      subtype = (self.eqcol01>>4) >> 2
      color = 10320704
      if(subtype == 1)
        color = 544124
      if(subtype == 2)
        color = 5153501
      if(subtype == 3)
        color = 12246606
      angle = RollDice(1, 256)*1.40625
      offx = sin(angle)
      offy = sin(angle+90.0)
      vx = offx*0.1
      vy = offy*0.1
      if(self.action == ACTION_JUMP)
        // Randomly lose feathers and play the flap sound too...
        if(RollDice(1, 100) > 25)
          PFEATHER.Effect(self.x+offx, self.y+offy, self.z+1.5, vx, vy, color)
        if(RollDice(1, 100) > 25)
          PFEATHER.Effect(self.x-offx, self.y-offy, self.z+1.5, -vx, -vy, color)
        // !!!BAD!!!
        // !!!BAD!!!  Do the flap sound...
        // !!!BAD!!!
      else
        // Lose one feather...
        PFEATHER.Effect(self.x+offx, self.y+offy, self.z+1.5, vx, vy, color)
      return TRUE
  if(self.event & 2)
    // Second Frame Event means stop flying
    self.flags = self.flags & (65535 - CHAR_HOVER_ON)
    self.flags = self.flags | CHAR_GRAVITY_ON
    self.vspin = 1024
    return TRUE
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2


  ModelClear()
  if(subtype == 0)
    // Eagle Eye
    ModelAssign(self+MODEL_BASE_FILE, "FILE:CARRION.RDY")
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_BASE_TEX2, "FILE:ORANGE.RGB")
    ModelAssign(self+MODEL_BASE_TEX3, "FILE:DYELLOW.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)

    ModelAssign(self+MODEL_HEAD_FILE, "FILE:CARHEAD0.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:WHITE.RGB")
  if(subtype == 1)
    // Carrion Mage
    ModelAssign(self+MODEL_BASE_FILE, "FILE:CARRION.RDY")
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_BASE_TEX3, "FILE:BLACK.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)

    ModelAssign(self+MODEL_HEAD_FILE, "FILE:CARHEAD1.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:FLESH.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:WHITE.RGB")
  if(subtype == 2)
    // Feather Knight
    ModelAssign(self+MODEL_BASE_FILE, "FILE:CARRION.RDY")
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_BASE_TEX2, "FILE:CYAN.RGB")
    ModelAssign(self+MODEL_BASE_TEX3, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)

    ModelAssign(self+MODEL_HEAD_FILE, "FILE:CARHEAD0.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:WHITE.RGB")
  if(subtype == 3)
    // Tengu
    ModelAssign(self+MODEL_BASE_FILE, "FILE:CARRION.RDY")
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_BASE_TEX2, "FILE:PURPLE.RGB")
    ModelAssign(self+MODEL_BASE_TEX3, "FILE:GREEN.RGB")

    ModelAssign(self+MODEL_HEAD_FILE, "FILE:CARHEAD0.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:MAGENTA.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:WHITE.RGB")



  ModelAssign(self+MODEL_BASE_COLOR, WHITE)
  ModelAssign(self+MODEL_HEAD_COLOR, WHITE)
  STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
  if((self.eqleft & 248) == ITEM_BOW)
    STANDARD.ItemModelSetup(MODEL_LEFT_FILE)
  STANDARD.ItemModelSetup(MODEL_RIGHT_FILE)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2

  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Eagle Eye", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Carrion Mage", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Feather Knight", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Tengu", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  int self, target, index, hand, i, found, item_type
  float x, y, distance, min_dis, max_dis
  self = FindSelf()


  // Ranged state checks...
  if(self.aistate == AISTATE_RANGED_FIRE || self.aistate == AISTATE_RANGED_AIM)
    // Check for quiver in inventory...  Also check mana...
    found = FALSE
    i = 224
    if(self.mana > 1)
      found = TRUE
      i = 255
    while(i < 246)
      if(i != 240 && i != 241)
        item_type = FileReadByte(self, i)
        if(item_type >= ITEM_QUIVER_50 && item_type <= ITEM_QUIVER_1)
          found = TRUE
          i = 255
      i++
    if(!found)
      // Out of ammo...
      if(self.eqright == ITEM_NONE)
        // Draw our secondary weapon...
        ButtonPress(BUTTON_PRESS, BUTTON_RIGHT, 0)
      self.aitimer = 1
      self.aistate = 0
      return TRUE



  // Face our target as best we're able...
  if(self.aistate == AISTATE_RANGED_AIM)
    self.aistate = 0
    target = FindTarget()
    if(target)
      self.gotox = (target.x + self.x)*0.5
      self.gotoy = (target.y + self.y)*0.5
      self.aitimer = 20
      self.aistate = AISTATE_RANGED_FIRE
      STANDARD.AIPitAvoidance()
      return TRUE


  // Stay put and fire...
  if(self.aistate == AISTATE_RANGED_FIRE)
    // Continue with our ranged attack...
    self.aistate = 0
    hand = RollDice(1, 2)
    self.gotox = self.x
    self.gotoy = self.y
    min_dis = 5.0
    max_dis = 30.0
    if(hand == 1)
      // Check for enchant-friendly type of spells...
      if(self.eqleft == ITEM_SPELL_ENCHANT || self.eqleft == ITEM_SPELL_DEFLECT || self.eqleft == ITEM_SPELL_ICE_BRAND || self.eqleft == ITEM_SPELL_FLAME_BRAND)
        if(RollDice(1, 100) < 25)
          self.target = FindIndex(self)
          target = FindTarget()
        else
          self.target = AcquireTarget(ACQUIRE_SEE_BEHIND | ACQUIRE_SEE_FRIENDLY, NULL, 75.0)
          target = FindTarget()
          if(!target)
            self.target = FindIndex(self)
            target = FindTarget()
        ButtonPress(BUTTON_PRESS, BUTTON_LEFT, 0)
        self.aitimer = 20
        STANDARD.AIPitAvoidance()
        return TRUE
    if((self.eqleft & 248) == ITEM_BOW)
      hand = 1
      max_dis = 35.0
      if((self.eqleft & 252) == ITEM_BOW)
        max_dis = 20.0
    if(self.eqleft == ITEM_SPELL_ANIMATE_DEAD && hand == 1)
      max_dis = 15.0
    if(self.eqleft == ITEM_SPELL_TELEPORT && hand == 1)
      max_dis = 20.0
    if(self.eqleft == ITEM_SPELL_ENTANGLE && hand == 1)
      max_dis = 10.0
    if(self.eqspec2 == ITEM_SPELL_KNOCK && hand == 2)
      max_dis = 10.0
    self.target = AcquireTarget(ACQUIRE_SEE_BEHIND | ACQUIRE_SEE_ENEMY, NULL, 75.0)
    target = FindTarget()
    if(target)
      if(LineOfSight(target.x, target.y, target.z+target.height))
        self.gotox = (target.x + self.x)*0.5
        self.gotoy = (target.y + self.y)*0.5
        x = target.x - self.x
        y = target.y - self.y
        distance = x*x + y*y
        if(distance > min_dis*min_dis)
          if(distance < max_dis*max_dis)
            if(GetDirection(target.x, target.y) == DIRECTION_FRONT)
              self.aistate = AISTATE_RANGED_FIRE
              self.aitimer = 5 + RollDice(1, 10)
              if(hand == 1)
                if(self.eqleft == ITEM_SPELL_TELEPORT)
                  self.gotox = target.x+(self.frontx*4.0)
                  self.gotoy = target.y+(self.fronty*4.0)
                  self.aistate = 0
                  self.aitimer = 1
                if(self.eqleft == ITEM_SPELL_FIRESTORM)
                  self.gotox = target.x
                  self.gotoy = target.y
                ButtonPress(BUTTON_PRESS, BUTTON_LEFT, 0)
              else
                ButtonPress(BUTTON_PRESS, BUTTON_SPECIAL2, 0)
              STANDARD.AIPitAvoidance()
              return TRUE


  // Randomly enter the ranged attack state...
  if(RollDice(1, 100) < 30)
    self.aistate = AISTATE_RANGED_AIM
    self.aitimer = 1
    return TRUE


  // Randomly fly around...
  if(RollDice(1, 100) < 10)
    ButtonPress(BUTTON_PRESS_AND_HOLD, BUTTON_SPECIAL1, 40)


  STANDARD.AIUseTripe()
  STANDARD.AIUsePotions()
  STANDARD.AIScript()


//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2

  self.boxsize = 1.8
  self.height = 4
  self.flags = CHAR_GRAVITY_ON
  self.vflags = VIRTUE_FLAG_EATS_TRIPE
  self.vspin = 1024
  self.timer = 1
  self.aitimer = 1

  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 10, 1.1, ITEM_HEAL, ITEM_MEAT)
  self.hitsmax = 12*difficulty
  self.hits = self.hitsmax
  self.str = (6*difficulty)+8
  self.dex = (5*difficulty)+5
  self.int = (4+difficulty)+7


  if(subtype == 0)
    self.eqleft = ITEM_BOW
    self.item00 = ITEM_SHORT_SWORD
    self.item01 = ITEM_QUIVER_50+RollDice(1, 10)-1
    self.item02 = ITEM_QUIVER_50+RollDice(1, 10)-1
    if(self.dex < 10)
      self.dex = 10
  if(subtype == 1)
    self.eqleft = ITEM_SPELL_ANIMATE_DEAD
    if(RollDice(1, 100)<50)
      if(RollDice(1, 100)<50)
        self.eqleft = ITEM_SPELL_DEFLECT
      else
        self.eqleft = ITEM_SPELL_ENCHANT
    self.eqspec2 = ITEM_SPELL_MISSILE
    if(RollDice(1, 100)<50)
      self.eqspec2 = ITEM_SPELL_FIREBALL
    if(RollDice(1, 100)<50)
      self.eqspec2 = ITEM_SPELL_LIGHTNING_BOLT


    self.eqright = ITEM_MACE
    if(self.str < 20)
      self.str = 20
    if(self.int < 25)
      self.int = 25
    self.manamax = 15
    self.mana = self.manamax
  if(subtype == 2)
    self.eqleft = ITEM_LONG_BOW
    self.item00 = ITEM_SWORD
    self.item01 = ITEM_QUIVER_50+RollDice(1, 10)-1
    self.item02 = ITEM_QUIVER_50+RollDice(1, 10)-1
    self.item03 = ITEM_QUIVER_50+RollDice(1, 10)-1
    self.item04 = ITEM_QUIVER_50+RollDice(1, 10)-1
    if(self.dex < 15)
      self.dex = 15
  if(subtype == 3)
    self.eqleft = ITEM_SPELL_TELEPORT
    if(RollDice(1, 100)<50)
      if(RollDice(1, 100)<50)
        self.eqleft = ITEM_SPELL_DISENCHANT
      else
        self.eqleft = ITEM_SPELL_ICE_BRAND
    self.eqspec2 = ITEM_SPELL_PETRIFY
    if(RollDice(1, 100)<50)
      self.eqspec2 = ITEM_SPELL_ICEBALL
    self.eqright = ITEM_GREAT_SWORD


    self.int = 30
    self.dex = 30
    self.manamax = 30
    self.mana = self.manamax



  self.defice = subtype*2


  STANDARD.FixStats(self)
  ModelSetup()


//-----------------------------------------------------------------------
