//-----------------------------------------------------------------------
int AIScript()
  int self, target
  float x, y, dis, dot
  self = FindSelf()



  // It shouldn't ever do this, but it does for some reason...
  if(self.sflags & SCRIPT_BOSS_DEFEAT)
    self.aitimer = 1000
    return TRUE


  // Pit rise thing using PTRAP...
  if(self.aistate == 255)
    self.gotox = self.x
    self.gotoy = self.y
    ButtonPress(BUTTON_PRESS_AND_HOLD, BUTTON_LEFT, 200)
    self.aistate = 0
    self.aitimer = 300
    self.hoverz = 2.0
    return FALSE


  if(AISwoopAndRise(20))
    return FALSE


  self.aistate = 0 //STANDARD_AISTATE_ATTACK
  STANDARD.AIUsePotions()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()



  // Breath attack...
  target = FindTarget()
  if(target)
    if(target.team != self.team && target.team != TEAM_NEUTRAL)
      x = target.x - self.x
      y = target.y - self.y
      dis = x*x + y*y
      if(dis < 400.0)
        ButtonPress(BUTTON_PRESS_AND_HOLD, BUTTON_SPECIAL2, 0)
        x = target.x - (self.x + self.frontx*5.0)
        y = target.y - (self.y + self.fronty*5.0)
        dot = x*self.frontx + y*self.fronty
        if(dot > 0.0 && self.charge > 128 && dis < 200.0)
          ButtonPress(BUTTON_UNPRESS, BUTTON_SPECIAL2, 0)
  else
    self.bflags = 0
    self.charge = 0



//-----------------------------------------------------------------------
int AISwoopAndRise(int duration)
  int self, target
  self = FindSelf()


  // Swoop and rise thing...
  if(self.aistate == 254)
    self.aistate = 0
    self.aitimer = 15
    target = FindTarget()
    if(target)
      if(self.z > target.z+1.0 && self.floorz > (target.floorz-1.0))
        // Swoop...
        ButtonPress(BUTTON_PRESS_AND_HOLD, BUTTON_RIGHT, duration)
        self.aistate = 254
        self.gotox = target.x
        self.gotoy = target.y
        FindPath()
      else
        if(self.z < target.z+5.0)
          // Rise...
          ButtonPress(BUTTON_PRESS_AND_HOLD, BUTTON_LEFT, duration<<1)
      return TRUE
  // Randomly swoop and rise...
  if(RollDice(1, 100) < 20)
    self.aistate = 254
    self.aitimer = 5
    return TRUE
  return FALSE


//-----------------------------------------------------------------------
int ChargeEffect(float x, float y, float z, int charge)
  int self, subtype

  self = FindSelf()
  subtype = ((self.eqcol01>>4) * 5)>>4
  if(subtype == 0)
    // Red Dragon
    PFIRBALL.ChargeEffect(x, y, z, charge)
  if(subtype == 1)
    // Green Dragon
    PPSNBALL.ChargeEffect(x, y, z, charge)
  if(subtype == 2)
    // Blue Dragon
    PZAPPY.ChargeEffect(x, y, z, charge)
  if(subtype == 3)
    // Black Dragon
    PACDBALL.ChargeEffect(x, y, z, charge)
  if(subtype == 4)
    // White Dragon
    PICEBALL.ChargeEffect(x, y, z, charge)

//-----------------------------------------------------------------------
int ShotEffect(float x, float y, float z, float vx, float vy, float vz, int charge)
  int self, subtype


  self = FindSelf()
  subtype = ((self.eqcol01>>4) * 5)>>4
  if(subtype == 0)
    // Red Dragon
    PFIRBALL.ShotEffect(x, y, z, vx, vy, vz, FindIndex(self), charge)
  if(subtype == 1)
    // Green Dragon
    PPSNBALL.ShotEffect(x, y, z, vx, vy, vz, FindIndex(self), charge)
  if(subtype == 2)
    // Blue Dragon
    PZAPPY.ShotEffect(x+(self.vx*6.0), y+(self.vy*6.0), z, vx, vy, vz, FindIndex(self), charge)
  if(subtype == 3)
    // Black Dragon
    PACDBALL.ShotEffect(x, y, z, vx, vy, vz, FindIndex(self), charge)
  if(subtype == 4)
    // White Dragon
    PICEBALL.ShotEffect(x, y, z, vx, vy, vz, FindIndex(self), charge)


//-----------------------------------------------------------------------
int ResetManaTimer()
  int self, subtype


  self = FindSelf()
  subtype = ((self.eqcol01>>4) * 5)>>4
  if(subtype == 0)
    // Red Dragon - Slow Recharge
    self.tetimer = 450
  if(subtype == 1)
    // Green Dragon - Normal Recharge
    self.tetimer = 300
  if(subtype == 2)
    // Blue Dragon - Fast Recharge
    self.tetimer = 150
  if(subtype == 3)
    // Black Dragon - Normal Recharge
    self.tetimer = 300
  if(subtype == 4)
    // White Dragon - Slow Recharge
    self.tetimer = 450


//-----------------------------------------------------------------------
int SetupStats()
  int self, subtype


  self = FindSelf()
  subtype = ((self.eqcol01>>4) * 5)>>4
  self.defedge = 6
  self.defbash = 4
  if(subtype == 0)
    // Red Dragon - Slow Recharge - Super strong...
    self.hitsmax = 40
    self.manamax = 10
    self.str = 50
    self.dex = 15
    self.int = 15
    self.defedge = self.defedge + 4
    self.defbash = self.defbash + 2
    self.defice = -4
    self.deffire = 8
    self.defacid = 4
    self.defvolt = 4
  if(subtype == 1)
    // Green Dragon - Normal Recharge - Fast
    self.hitsmax = 30
    self.manamax = 10
    self.str = 15
    self.dex = 30
    self.int = 15
    self.defice = 4
    self.deffire = 4
    self.defacid = 8
    self.defvolt = 4
    self.sflags = self.sflags | SCRIPT_NO_POISON
  if(subtype == 2)
    // Blue Dragon - Fast Recharge - Intelligent...
    self.hitsmax = 20
    self.manamax = 10
    self.str = 15
    self.dex = 15
    self.int = 30
    self.defice = 4
    self.deffire = 4
    self.defacid = 4
    self.defvolt = 8
  if(subtype == 3)
    // Black Dragon - Normal Recharge - Intelligent
    self.hitsmax = 20
    self.manamax = 10
    self.str = 15
    self.dex = 15
    self.int = 50
    self.defice = 4
    self.deffire = 4
    self.defacid = 8
    self.defvolt = 4
  if(subtype == 4)
    // White Dragon - Slow Recharge - Balanced...
    self.hitsmax = 30
    self.manamax = 10
    self.str = 30
    self.dex = 30
    self.int = 30
    self.defice = 8
    self.deffire = -4
    self.defacid = 4
    self.defvolt = 4
  self.hitsmax = self.hitsmax + RollDice(2, 6)
  self.manamax = self.manamax + RollDice(2, 6)
  self.str = self.str + RollDice(2, 6)
  self.dex = self.dex + RollDice(2, 6)
  self.int = self.int + RollDice(2, 6)
  self.hits = self.hitsmax
  self.mana = self.manamax


//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, cost, i, subtype
  float x, y, z, floorz
  float vx, vy, vz



  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  event = self.event & 7
  if((self.event & 8))
    ButtonHandled()
    if(event == BUTTON_SPECIAL1 || event == BUTTON_SPECIAL2)
      if(self.action == ACTION_JUMP)
        if(self.mana > 0)
          if(self.charge < 64)
            self.charge = 64
          cost = (self.charge>>6)
          if(cost > self.mana)
            cost = self.mana
            self.charge = cost<<6
          self.mana = self.mana - cost


          self.action = ACTION_BASH_LEFT
          x = self.x + self.frontx*3.4
          y = self.y + self.fronty*3.4
          z = self.z + 2.8
          subtype = ((self.eqcol01>>4) * 5)>>4
          if(subtype == 2)
            AutoAim(3.0, 6.0, self.spin, self.team, self.dex, 8192, AUTOAIM_CRUNCH)
          else
            AutoAim(0.5, 0.25, self.spin, self.team, self.dex, 8192, AUTOAIM_CRUNCH)
          vx = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_X)
          vy = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Y)
          vz = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Z)


          ShotEffect(x, y, z, vx, vy, vz, self.charge)
      self.charge = 0
      return 0
  else
    if(STANDARD.RiderDismountButton())
      return 0
    if(event == BUTTON_LEFT)
      floorz = RoomWaterLevel()
      if(floorz < self.floorz)
        floorz = self.floorz
      if(floorz < 0.0)
        floorz = 0.0
      if(self.hoverz < (floorz+20.0))
        self.hoverz = self.z + 2.00
      return 0
    if(event == BUTTON_RIGHT)
      floorz = RoomWaterLevel()
      if(floorz < self.floorz)
        floorz = self.floorz
      if(self.hoverz > (floorz+3.3))
        self.hoverz = self.hoverz - 0.60
      else
        // Make able to scrape ground to grab keys & stuff...
        self.vz = self.vz - 0.04
      return 0
    if(event == BUTTON_SPECIAL1 || event == BUTTON_SPECIAL2)
      if(self.action == ACTION_JUMP || self.action == ACTION_BASH_RIGHT)
        // Do breath weapon charging...
        x = self.x + self.frontx*3.1
        y = self.y + self.fronty*3.1
        z = self.z + 2.3


        // Increment the charge
        i = 0
        while(i < 4)
          if(self.charge < 255 && self.charge < (self.mana<<6))
            self.charge++
          i++


        ChargeEffect(x, y, z, self.charge)
        return 0


//-----------------------------------------------------------------------
int Event()
  int self, index, oldtarget, target, damage, spin
  float oldvx, oldvy, speed



  STANDARD.DamageSoundEvent("FILE:-DRAG01.RAW", "FILE:-DRAG02.RAW", "FILE:-DRAG03.RAW")


  // Make mount/dismount work nicer...
  self = FindSelf()
  spin = self.spin
  if(self.event == EVENT_MOUNTED)
    self.aitimer = 0
    return 0
  if(self.event == EVENT_DISMOUNTED)
    index = FindIndex(self)
    if(IndexIsLocalPlayer(index) == FALSE)
      // Dragon isn't a player, so turn it's AI back on...
      self.aitimer = 20
    return 0


  // Mana recharge...
  if(self.event == EVENT_THIRDIARY_TIMER)
    if(self.mana < (self.manamax-self.manatap))
      self.mana++
    ResetManaTimer()


  // Normal stuff...
  if(self.action == ACTION_JUMP || self.action == ACTION_BASH_RIGHT)
    oldvx = self.vx
    oldvy = self.vy
    if(self.event == EVENT_HIT_CHARACTER)
      oldtarget = self.target
      self.target = SystemGet(SYS_COLLISIONCHAR, 0, 0)
      target = FindTarget()
      self.target = oldtarget
      if(target)
        speed = sqrt(oldvx*oldvx + oldvy*oldvy + self.vz*self.vz)
        damage = (7.0*speed)
        damage = damage+(self.str>>4)+1
        if(STANDARD.BumpDamageEvent(DAMAGE_EDGE, damage, 1, TRUE, FALSE))
          self.spin = self.spin+32768
          self.action = ACTION_BASH_RIGHT
          self.vx = oldvx
          self.vy = oldvy
          if(target.nctimer < 40)
            target.nctimer = 40
          self.rltimer = 5
          SystemSet(SYS_BUMPABORT, 0, 0, 0)
          return 0
  STANDARD.Event()
  STANDARD.LevelUpEvent(3, 2, 1, 2, 2)


  // Boss Defeat stuff...
  if(self.action == ACTION_KNOCK_OUT_BEGIN)
    STANDARD.BossDefeatStart()
    self.spin = spin
  else
    if(self.action == ACTION_STUN_BEGIN)
      self.daction = ACTION_JUMP


//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()


//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype

  self = FindSelf()
  subtype = ((self.eqcol01>>4) * 5)>>4
  ModelAssign(self+MODEL_BASE_FILE, "FILE:DRAGON.RDY")
  if(subtype == 0)
    // Red Dragon
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:RED.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:YELLOW.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, 4210864)
  if(subtype == 1)
    // Green Dragon
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:YELLOW.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, 4206768)
  if(subtype == 2)
    // Blue Dragon
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DBLUE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:RED.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, 2105536)
  if(subtype == 3)
    // Black Dragon
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:BLACK.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:PURPLE.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, 9441520)
  if(subtype == 4)
    // White Dragon
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, 4239600)
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:DRAGON.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)
  return TRUE


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()

  subtype = ((self.eqcol01>>4) * 5)>>4
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Red Dragon", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Green Dragon", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Blue Dragon", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Black Dragon", 256)
  if(subtype == 4)
    StringAppend(NAME_STRING, "White Dragon", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int Refresh()
  if(!STANDARD.BossDefeatRefresh())
    STANDARD.Refresh()


//-----------------------------------------------------------------------
int Spawn()
  int self
  self = FindSelf()

  self.boxsize = 1.8
  self.height = 7
  self.flags = CHAR_FUZZY_ON | CHAR_HOVER_ON | CHAR_FINISH_ACTION | CHAR_CAN_BE_MOUNTED | CHAR_NO_BACKSTAB
  self.sflags = SCRIPT_NO_KNOCK_OUT | SCRIPT_BUMP_DAMAGE | SCRIPT_NO_PETRIFY
  self.vflags = VIRTUE_FLAG_IMMUNE | VIRTUE_FLAG_BUMP_IMMUNE | VIRTUE_FLAG_AFTER_WATER | VIRTUE_FLAG_EATS_TRIPE
  self.oflags = OTHER_FLAG_NO_MORPH
  self.vspin = 700
  self.timer = 1
  self.charge = 0

  self.hoverz = self.floorz + 3.0

  self.action = ACTION_JUMP
  self.daction = ACTION_JUMP
  self.flyfric = 0.995
  SetupStats()
  ResetManaTimer()
  STANDARD.TreasureSetup(1.0, self.hitsmax*5, 0.0, ITEM_NONE, ITEM_NONE)



// !!!BAD!!!
// !!!BAD!!! Pit rise thing
// !!!BAD!!!
  if(self.z < -10.0)
    self.aistate = 255
// !!!BAD!!!
// !!!BAD!!!
// !!!BAD!!!



  ModelSetup()
  STANDARD.PutOnMount()


//-----------------------------------------------------------------------
