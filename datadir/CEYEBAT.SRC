//-----------------------------------------------------------------------
int ButtonEvent()
  int self, color, subtype, dtype, damage, wound, special_flags
  float floorz, vx, vy, vz, width




  self = FindSelf()
  subtype = (self.eqcol01>>6)
  if((self.event & 8))
    if((self.event&7) == BUTTON_SPECIAL2)
      self.vspin = self.vspin + 150
      if(self.vspin > 700)
        self.vspin = 700
//        ButtonHandled()
//        return 0
      else
        // Fall through to make beam spawn...
        self.event = self.event & 7


  if(STANDARD.RestrictedButtonEvent())
    return TRUE




  if(!(self.event & 8))
    if(self.event == BUTTON_LEFT)
      floorz = RoomWaterLevel()
      if(floorz < self.floorz)
        floorz = self.floorz
      if(floorz < 0.0)
        floorz = 0.0
      if(self.hoverz < (floorz+20.0))
        self.hoverz = self.z + 1.00
      return 0
    if(self.event == BUTTON_RIGHT)
      floorz = RoomWaterLevel()
      if(floorz < self.floorz)
        floorz = self.floorz
      if(self.hoverz > (floorz+1.5))
        self.hoverz = self.hoverz - 0.30
      else
        // Make able to scrape ground to grab keys & stuff...
        self.vz = self.vz - 0.02
      return 0
    if(self.event == BUTTON_SPECIAL2)
      if(self.daction == ACTION_JUMP)
        if(self.mana > 0)
          dtype = DAMAGE_FIRE
          damage = 0
          wound = 0
          if(subtype == 0)
            color = 6405161
            width = 0.001
            special_flags = SPECIAL_FLAG_POISON
          if(subtype == 1)
            color = 14076853
            width = 0.003
            special_flags = SPECIAL_FLAG_PETRIFY
          if(subtype == 2)
            color = 15708729
            width = 0.0023
            damage = 1
            wound = 1
            special_flags = 0
          if(subtype == 3)
            color = 2193862
            width = 0.0016
            dtype = DAMAGE_ICE
            damage = 2
            wound = 1
            special_flags = SPECIAL_FLAG_DRAIN
          vx = self.frontx*0.5
          vy = self.fronty*0.5
          vz = (self.hoverz - 5.0) * -0.02
          if(vz > 0.0)
            vz = 0.0
          width = (900-self.vspin)*width

          PBEAM.Effect(vx, vy, vz, 1.3, width, color, dtype, damage, wound, FindIndex(self), special_flags)

          if(self.vspin > 100)
            self.vspin = self.vspin - 100
            if(self.vspin < 100)
              self.vspin = 100


          if(self.tetimer < 10)
            self.tetimer = 40
            if(self.vspin < 400)
              self.mana--
        else
          self.vspin = 700
          ButtonHandled()
      return 0


//-----------------------------------------------------------------------
int Event()
  int self, petrify, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  STANDARD.DamageSoundEvent("FILE:-RAT01.RAW", NULL, NULL)
  if(self.event == EVENT_THIRDIARY_TIMER)
    if(self.mana < (self.manamax-self.manatap))
      self.mana++
    self.tetimer = 30
    return 0
  petrify = self.pttimer
  if(self.event == EVENT_PETRIFIED)
    self.hits = 0
    self.action = ACTION_KNOCK_OUT_BEGIN
    self.daction = ACTION_KNOCK_OUT
    self.flags = self.flags | CHAR_FALL_ON
    self.nctimer = 30
    self.vz = 0.60
  STANDARD.BumpDamageEvent(DAMAGE_EDGE, (2+subtype), 1, FALSE, TRUE)
  STANDARD.Event()
  STANDARD.LevelUpEvent(4, 1, 1, 4, 0)
  if(self.action == ACTION_KNOCK_OUT_BEGIN)
    self.flags = (self.flags & (65535-CHAR_HOVER_ON)) | CHAR_GRAVITY_ON
    self.pttimer = petrify
  else
    if(self.action == ACTION_STUN_BEGIN)
      self.action = ACTION_JUMP
      self.daction = ACTION_JUMP
  if(subtype == 3)
    self.hitstap = 0


//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype, color
  self = FindSelf()
  subtype = (self.eqcol01>>6)
  color = (self.eqcol01)&15


  ModelAssign(self+MODEL_BASE_FILE, "FILE:BATANIM.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:DORANGE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:YELLOW.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=BAT.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:=EYE1.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, BLUE)


  if(subtype == 0)
    // Eye Bat (Poison)
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:EYEBAT.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:PURPLE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:YELLOW.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=BAT.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=EYEBAT.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, BLUE)
  if(subtype == 1)
    // Petriff Eye (Stone)
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:EYEBAT.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:GREEN.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=BAT.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=EYEBAT.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, GREEN)
  if(subtype == 2)
    // Sentinel (Damage)
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:EYEBAT.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:RED.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:YELLOW.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=BAT.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=EYEBAT.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, BLUE)
  if(subtype == 3)
    // Magic Eye (Drain)
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:EYEBAT.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=BAT.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=EYEBAT.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Eye Bat", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Petriff Eye", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Sentinel", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Magic Eye", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIUseBeam()
  int self, target
  float x, y, dot, dis

  self = FindSelf()
  target = FindTarget()
  if(target && (self.mana >= (self.manamax>>1)))
    if(RollDice(1, 10) < 3)
      if(self.z > (target.z-2.0) && (self.z+self.height) < (target.z+target.height+2.0))
        x = target.x - self.x
        y = target.y - self.y
        dis = sqrt(x*x + y*y)
        if(dis > 0.01)
          x = x/dis
          y = y/dis
          dot = x*self.frontx + y*self.fronty
          if(dot > 0.7)
            ButtonPress(BUTTON_PRESS_AND_HOLD, BUTTON_SPECIAL2, 60)
            self.aitimer = 60
            self.gotox = target.x
            self.gotoy = target.y


//-----------------------------------------------------------------------
int AIScript()
  if(CDRAGON.AISwoopAndRise(30))
    return FALSE
  STANDARD.AIUseTripe()
  STANDARD.AIScript()
  AIUseBeam()


//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>6)


  self.boxsize = 1.7
  self.height = 3
  self.flags = CHAR_FUZZY_ON | CHAR_HOVER_ON | CHAR_FINISH_ACTION
  self.sflags = SCRIPT_NO_KNOCK_OUT | SCRIPT_BUMP_DAMAGE
  self.vspin = 700
  self.timer = 0  // Unused
  self.tetimer = 1  // Used for mana drain/regain...
  self.aitimer = 1
  self.hoverz = self.floorz + 3.0
  self.action = ACTION_JUMP
  self.daction = ACTION_JUMP
  self.flyfric = 0.995



  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 25, 0, ITEM_NONE, ITEM_MEAT)
  self.hitsmax = 3*difficulty+3
  self.hits = self.hitsmax
  self.manamax = (1*difficulty)+5
  self.mana = self.manamax
  self.dex = 2*difficulty+6
  self.str = 1*difficulty+4
  self.int = 3*difficulty+15
  self.defbash = subtype-1
  self.defedge = subtype-3
  if(subtype == 0)
    self.defacid = 2
  if(subtype == 1)
    self.deffire = 2
  if(subtype == 2)
    self.defacid = 1
  if(subtype == 3)
    self.deffire = -2
    self.defice = 2
    self.eflags = ENCHANT_FLAG_DEFLECT


  ModelSetup()

//-----------------------------------------------------------------------
