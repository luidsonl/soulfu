//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  event = self.event & 7
  if(!(self.event & 8))
    if(event == BUTTON_LEFT || event == BUTTON_RIGHT)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        self.action = ACTION_THRUST_RIGHT
        self.daction = ACTION_STAND
        ButtonHandled()
      return 0


//-----------------------------------------------------------------------
int Event()
  int self, child, subtype

  self = FindSelf()
  STANDARD.DamageSoundEvent("FILE:-RAT01.RAW", NULL, NULL)
  STANDARD.Event()
  STANDARD.LevelUpEvent(4, 0, 2, 4, 0)
  if(self.event == EVENT_HIT_WATER)
    // Spawn a helper...
    subtype = self.eqcol01>>6
    if((RoomWaterType() == WATER_TYPE_WATER && subtype < 3) || (RoomWaterType() == WATER_TYPE_LAVA && subtype == 3))
      if(ChildCount(self) < MaxHelpers(self, (subtype*2)+1) && self.hits > 1)
        SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TEAM, self.team)
        SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_SUBTYPE, self.eqcol01)
        SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_OWNER, FindIndex(self))
        if(FindByIndex(self.owner))
          SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_OWNER, self.owner)
        child = Spawn(CHARACTER, self.x, self.y, self.z, 0)
        if(child)
          child.aitimer = 30
          child.spin = self.spin
          SetupTargetMatrix(child)

          self.hitsmax = self.hitsmax>>1
          self.hits = self.hits>>1
          self.hitstap = self.hitstap>>1
          child.hitsmax = self.hitsmax
          child.hitstap = self.hitstap
          child.hits = self.hits
          self.expgive = ((self.expgive*5)>>3)+1
          child.expgive = self.expgive
          self.vz = 0.5 + RollDice(1, 10)*0.03
          child.vz = 0.5 + RollDice(1, 10)*0.03
          child.money = 0
          child.keys = 0
          child.item00 = ITEM_NONE

          self.action = ACTION_JUMP_BEGIN
          self.daction = ACTION_JUMP
          self.flags = self.flags | CHAR_FALL_ON
          child.action = ACTION_JUMP_BEGIN
          child.daction = ACTION_JUMP
          child.flags = child.flags | CHAR_FALL_ON


          if(subtype == 3)
            // Lava makes a red poof...
            PFLASH.Effect(self, "FILE:LITESPRK.RGB", DARK_RED, TRUE, 0.9)
            PFLASH.Effect(self, "FILE:=MPDUST.RGB", RED, FALSE, 1.0)
          else
            // Water makes a blue poof...
            PFLASH.Effect(self, "FILE:LITESPRK.RGB", DARK_BLUE, TRUE, 0.9)
            PFLASH.Effect(self, "FILE:=MPDUST.RGB", BLUE, FALSE, 1.0)


//-----------------------------------------------------------------------
int FrameEvent()
  int self, damage, special_flags, subtype
  self = FindSelf()

  if(self.event & 8)
    subtype = self.eqcol01>>6
    damage = RollDice(1, subtype+1)
    special_flags = 0
    PATTACK.Effect(self.x-self.frontx*0.5, self.y-self.fronty*0.5, self.z+1.0, self.frontx*0.4, self.fronty*0.4, 0.0, DAMAGE_EDGE, damage, 1, 2.0, 0.0, 8, special_flags)
    PlaySound("FILE:SWIPE.RAW", 225 + RollDice(1, 50), 255)


//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6

  ModelAssign(self+MODEL_BASE_FILE, "FILE:GREMF.RDY")
  if(subtype == 0)
    // Gremf
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:GREEN.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DYELLOW.RGB")
  if(subtype == 1)
    // BAD
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  if(subtype == 2)
    // Papa Gremf
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:RED.RGB")
  if(subtype == 3)
    // Mini Divil
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:RED.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:RED.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=GREMF.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)


  ModelAssign(self+MODEL_RIGHT_FILE, "FILE:GREMFSPR.RDY")
  ModelAssign(self+MODEL_RIGHT_TEX0, "FILE:DYELLOW.RGB")
  ModelAssign(self+MODEL_RIGHT_TEX1, "FILE:STEEL.RGB")
  ModelAssign(self+MODEL_RIGHT_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_RIGHT_TEX3, "FILE:LITESWIP.RGB")
  ModelAssign(self+MODEL_RIGHT_COLOR, WHITE)
  if(subtype == 3)
    ModelAssign(self+MODEL_RIGHT_TEX0, "FILE:BLACK.RGB")



//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Gremlin", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Gremf", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Papa Gremf", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Mini Divil", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUsePotions()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = self.eqcol01>>6

  self.boxsize = 0.75
  self.height = 3
  self.flags = CHAR_GRAVITY_ON | CHAR_CAN_RIDE_MOUNT
  self.sflags = SCRIPT_NO_KNOCK_OUT
  self.vflags = VIRTUE_FLAG_RUN_ME_OVER | VIRTUE_FLAG_EATS_TRIPE
  self.oflags = OTHER_FLAG_EASY_ENTANGLE
  self.vspin = 1024
  self.timer = 1


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 4, 2.0, ITEM_COOKIE, ITEM_HEAL)
  self.hitsmax = 2*difficulty+2
  self.hits = self.hitsmax
  self.str = (2*difficulty)+1
  self.dex = (6*difficulty)+3
  self.int = (4+difficulty)+2
  if(subtype == 1)
    self.defedge = 2
    self.defbash = 1
  if(subtype == 2)
    self.defedge = 4
    self.defbash = 2
    self.defacid = 2
    self.defice = 2
    self.deffire = 2
    self.defvolt = 2
  if(subtype == 3)
    self.defice = -4
    self.deffire = 4
  self.eqspec1 = ITEM_JUMP

  ModelSetup()
  STANDARD.PutOnMount()

//-----------------------------------------------------------------------
