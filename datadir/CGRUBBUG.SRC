//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.RestrictedButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self, damage, subtype, child, index
  self = FindSelf()
  subtype = self.eqcol01>>6
  damage = RollDice(1, subtype+1)

  STANDARD.DamageSoundEvent("FILE:-BUGHIT.RAW", NULL, NULL)
  STANDARD.BumpDamageEvent(DAMAGE_EDGE, damage, 1, FALSE, TRUE)
  STANDARD.Event()
  if(STANDARD.LevelUpEvent(4, 0, 2, 4, 0) == TRUE)
    // Change form...
    if(STANDARD.LevelUpChange("FILE:CGRUBMUM.RUN"))
      return TRUE



  if(self.event == EVENT_DAMAGED)
    if(self.daction == ACTION_STUN)
      self.evtimer = 40

//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6

  ModelAssign(self+MODEL_BASE_FILE, "FILE:GRUBBUG.RDY")
  if(subtype == 0)
    // Grub Bug
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:NGREEN.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:YELLOW.RGB")
  if(subtype == 1)
    // Puddle Bug
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:NDEEPBLU.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DMAGENTA.RGB")
  if(subtype == 2)
    // Crimson Bug
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:NRED.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DMAGENTA.RGB")
  if(subtype == 3)
    // Dusk Bug
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:GBLACK.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DBLUE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:SHADGRUB.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Grub Bug", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Puddle Bug", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Crimson Bug", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Dusk Bug", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty


  self = FindSelf()
  subtype = self.eqcol01>>6


  self.boxsize = 1.4
  self.height = 3
  self.flags = CHAR_GRAVITY_ON
  self.sflags = SCRIPT_NO_KNOCK_OUT | SCRIPT_BUMP_DAMAGE
  self.vflags = VIRTUE_FLAG_RUN_ME_OVER | VIRTUE_FLAG_EATS_TRIPE
  self.oflags = OTHER_FLAG_EASY_ENTANGLE
  self.timer = 1


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 2, 0.0, ITEM_NONE, ITEM_NONE)
  self.hitsmax = (2*difficulty)+1
  self.hits = self.hitsmax
  self.str = (2*difficulty)+3
  self.dex = (2*difficulty)+1
  self.int = (1+difficulty)+1
  self.defedge = subtype
  self.defbash = -3
  if(subtype == 1)
    self.defice = 2
  if(subtype == 2)
    self.deffire = 4
  if(subtype == 3)
    self.defacid = 4
  self.vspin = 256+(subtype*100)


  ModelSetup()

//-----------------------------------------------------------------------
