//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, child, subtype


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  event = self.event & 7
  if((self.event & 8))
    if(event == BUTTON_SPECIAL2)
      if(self.action == ACTION_SPECIAL_0)
        return 0
      if(self.action == ACTION_BLOCK || self.action == ACTION_BLOCK_BEGIN)
        self.action = ACTION_BLOCK_END
        self.daction = ACTION_STAND
      ButtonHandled()
    return 0
  else
    if(self.action == ACTION_BLOCK)
      if(event == BUTTON_LEFT || event == BUTTON_RIGHT || event == BUTTON_SPECIAL1)
        // Summon a helper grub...
        ButtonHandled()
        subtype = self.eqcol01>>6
        if(ChildCount(self) < MaxHelpers(self, subtype+2))
          self.action = ACTION_SPECIAL_0
          SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TEAM, self.team)
          SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_SUBTYPE, self.eqcol01)
          SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_OWNER, FindIndex(self))
          if(FindByIndex(self.owner))
            SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_OWNER, self.owner)
          child = Spawn(CHARACTER, self.x, self.y, self.z, "FILE:CGRUBBUG.RUN")
          if(child)
            child.aitimer = 30
            child.pstimer = self.pstimer
            child.spin = self.spin+32768
            child.vx = self.frontx * -0.5
            child.vy = self.fronty * -0.5
            child.vz = 0.25
            SetupTargetMatrix(child)
      return 0
    if(event == BUTTON_LEFT && self.eqleft == ITEM_NONE)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        if(self.rltimer == 0)
          self.action = ACTION_BASH_LEFT
          self.daction = ACTION_STAND
          if(self.dex < 50)
            self.rltimer = 50 - self.dex
          ButtonHandled()
      return 0
    if(event == BUTTON_RIGHT && self.eqright == ITEM_NONE)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        if(self.rrtimer == 0)
          self.action = ACTION_BASH_RIGHT
          self.daction = ACTION_STAND
          if(self.dex < 50)
            self.rrtimer = 50 - self.dex
          ButtonHandled()
      return 0
    if(event == BUTTON_SPECIAL2 && self.eqspec2 == ITEM_NONE)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        self.action = ACTION_BLOCK_BEGIN
        self.daction = ACTION_BLOCK
        ButtonHandled()
      return 0


//-----------------------------------------------------------------------
int Event()
  int self
  self = FindSelf()
  STANDARD.DamageSoundEvent("FILE:-BUGHIT.RAW", NULL, NULL)
  if(self.event == EVENT_HIT_FLOOR)
    if(self.daction == ACTION_JUMP)
      if(self.bflags & 2)
        // Still trying to block
        self.action = ACTION_SPECIAL_0
        self.daction = ACTION_BLOCK
      else
        // Normal landing
        self.action = ACTION_JUMP_END
        self.daction = ACTION_STAND
  STANDARD.Event()
  STANDARD.LevelUpEvent(6, 0, 0, 4, 0)

//-----------------------------------------------------------------------
int FrameEvent()
  int self, damage, special_flags
  self = FindSelf()

  if(self.event & 8)
    damage = RollDice(1, self.str+6) / 6
    special_flags = 0
    if(self.action == ACTION_BASH_LEFT)
      PATTACK.Effect(self.x-self.frontx*0.5+self.sidex*1.5, self.y-self.fronty*0.5+self.sidey*1.5, self.z+2.5, self.frontx*0.6-self.sidex*0.2, self.fronty*0.6-self.sidey*0.2, 0.0, DAMAGE_EDGE, damage, 1, 2.0, 0.5, 6, special_flags)
      PlaySound("FILE:SWIPE.RAW", 225 + RollDice(1, 50), 255)
    if(self.action == ACTION_BASH_RIGHT)
      PATTACK.Effect(self.x-self.frontx*0.5-self.sidex*1.5, self.y-self.fronty*0.5-self.sidey*1.5, self.z+2.5, self.frontx*0.6+self.sidex*0.2, self.fronty*0.6+self.sidey*0.2, 0.0, DAMAGE_EDGE, damage, 1, 2.0, 0.5, 6, special_flags)
      PlaySound("FILE:SWIPE.RAW", 225 + RollDice(1, 50), 255)

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6


  ModelAssign(self+MODEL_BASE_FILE, "FILE:GRUBMUM.RDY")
  if(subtype == 0)
    // Grub Mum
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:NGREEN.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:YELLOW.RGB")
  if(subtype == 1)
    // Puddle Mum
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:NDEEPBLU.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DMAGENTA.RGB")
  if(subtype == 2)
    // Crimson Mum
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:NRED.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DMAGENTA.RGB")
  if(subtype == 3)
    // Dusk Mum
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:GBLACK.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DBLUE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=GRUBMUM.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Grub Mum", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Puddle Mum", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Crimson Mum", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Dusk Mum", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  int self, subtype


  self = FindSelf()
  subtype = self.eqcol01>>6
  if(self.action == ACTION_BLOCK)
    ButtonPress(BUTTON_PRESS, BUTTON_LEFT, 30)
    self.aitimer = 50-(subtype*10)
    // Make sure we don't get stuck blocking...
    if(self.b3timer == 0)
      self.b3timer = 1
    return TRUE
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

  if(RollDice(1, 255) < 10)
    if(ChildCount(self) < MaxHelpers(self, subtype+2))
      // We can spawn a helper...
      ButtonPress(BUTTON_PRESS_AND_HOLD, BUTTON_SPECIAL2, 150)
      self.aitimer = 40

  


//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty


  self = FindSelf()
  subtype = self.eqcol01>>6
  self.boxsize = 2.4
  self.height = 4
  self.flags = CHAR_GRAVITY_ON
  self.vflags = VIRTUE_FLAG_EATS_TRIPE
  self.timer = 1


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 12, 0.0, ITEM_NONE, ITEM_NONE)
  self.hitsmax = (7*difficulty)+3
  self.hits = self.hitsmax
  self.str = (5*difficulty)+3
  self.dex = (3*difficulty)+2
  self.int = (1+difficulty)+2
  self.defedge = subtype
  self.defbash = -3
  if(subtype == 1)
    self.defice = 2
  if(subtype == 2)
    self.deffire = 4
  if(subtype == 3)
    self.defacid = 4
  self.vspin = 256+(subtype*100)
  self.eqspec1 = ITEM_JUMP



  ModelSetup()

//-----------------------------------------------------------------------
