#define DARK_VALUE  192
#define DARK_COLOR ((DARK_VALUE << 16) + (DARK_VALUE << 8) + (DARK_VALUE))

//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, color


  if(STANDARD.RestrictedButtonEvent())
    return TRUE
  self = FindSelf()


  event = self.event & 7
  if(!(self.event & 8))
    if(STANDARD.RiderDismountButton())
      return 0
    if(event == BUTTON_RIGHT || event == BUTTON_SPECIAL2)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND || (self.action >= ACTION_JUMP_BEGIN && self.action <= ACTION_JUMP_END))
        self.action = ACTION_BASH_LEFT
        self.daction = ACTION_BASH_RIGHT
        self.flags = self.flags | CHAR_FALL_ON
        self.vx = self.frontx*1.0
        self.vy = self.fronty*1.0
        self.vz = 0.5
        self.setimer = 60
        color = RGBColor(self.eqcol23 & 15)
        PBLUR.Effect(color, 2.0, 1.0, 220, 1.5)
        PlaySound("FILE:JUMP.RAW", 384, 255)
        ButtonHandled()
        return 0

//-----------------------------------------------------------------------
int Event()
  int self, index
  int target, oldtarget, damage, lurch, rider, mount, subtype
  float oldvx, oldvy

  self = FindSelf()
  subtype = (self.eqcol01>>7)
  if(subtype == 0)
    STANDARD.DamageSoundEvent("FILE:-KIT01.RAW", NULL, NULL)
  else
    STANDARD.DamageSoundEvent("FILE:-PUP01.RAW", NULL, NULL)
  if(self.event == EVENT_DISMOUNTED)
    index = FindIndex(self)
    if(IndexIsLocalPlayer(index) == FALSE)
      self.aitimer = 20
    self.flags = self.flags & (65535 - CHAR_CAN_BE_MOUNTED)
    return 0
  if(self.event == EVENT_SECONDARY_TIMER)
    if(self.daction == ACTION_BASH_RIGHT)
      self.daction = ACTION_STAND
      self.action = ACTION_THRUST_LEFT
  if(self.event == EVENT_HIT_FLOOR)
    if(self.daction == ACTION_BASH_RIGHT)
      self.daction = ACTION_STAND
      self.action = ACTION_THRUST_LEFT
      return TRUE
  if(self.event == EVENT_HIT_CHARACTER)
    oldtarget = self.target
    self.target = SystemGet(SYS_COLLISIONCHAR, 0, 0)
    target = FindTarget()
    if(target)
      // Let gremf mount...
      if(self.team == target.team)
        if(target.flags & CHAR_CAN_RIDE_MOUNT)
          if(CharacterScriptFile(self.target) == "FILE:CGREMF.RUN")
            rider = FindByIndex(self.rider)
            mount = FindByIndex(target.mount)
            if(rider == NULL && mount == NULL)
              if((target.daction == ACTION_JUMP && target.vz < 0.0) || target.daction == ACTION_STAND || target.daction == ACTION_WALK)
                target.mount = FindIndex(self)
                target.action = ACTION_RIDE
                target.daction = ACTION_RIDE
                target.bone = 10
                self.rider = self.target
                self.target = oldtarget
                self.flags = self.flags | CHAR_CAN_BE_MOUNTED
                self.aitimer = 0
                return 0
      oldvx = self.vx
      oldvy = self.vy
      damage = RollDice(1, self.str+6) / 6
      lurch = (self.daction == ACTION_BASH_RIGHT)
      if(lurch)
        damage = damage + 2
      STANDARD.BumpDamageEvent(DAMAGE_EDGE, damage, 1, lurch, TRUE)
    self.target = oldtarget
    return 0
  STANDARD.Event()
  STANDARD.LevelUpEvent(4, 0, 2, 4, 0)
  if(self.event == EVENT_DAMAGED)
    if(self.daction == ACTION_STUN)
      self.evtimer = (self.evtimer >> 1) + 1

//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype, color
  self = FindSelf()
  subtype = (self.eqcol01>>7)
  color = self.eqcol23 & 15


  ModelAssign(self+MODEL_BASE_FILE, "FILE:KITTANIM.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)


  if(subtype == 0)
    // Kitten
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:KITTEN.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, DullColor(color))
    ModelAssign(self+MODEL_CHEST_TEX1, DullColor(color))
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=KITTEN.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, DARK_COLOR)
  if(subtype == 1)
    // Puppy
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:PUPPY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, DullColor(color))
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:SHADPUPP.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=KITTEN.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)
  STANDARD.EyeSetup(self+MODEL_CHEST_TEX3, self.team)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 3

  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Kitten", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Puppy", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>7)
  self.eqcol23 = RollDice(1, 256)


  self.boxsize = 1.2
  self.height = 3
  self.flags = CHAR_GRAVITY_ON
  self.sflags = SCRIPT_NO_KNOCK_OUT | SCRIPT_BUMP_DAMAGE
  self.vflags = VIRTUE_FLAG_RUN_ME_OVER | VIRTUE_FLAG_EATS_TRIPE
  self.oflags = OTHER_FLAG_EASY_ENTANGLE
  self.vspin = 1024
  self.timer = 1


  STANDARD.TreasureSetup(1.0, 15, 0.0, ITEM_NONE, ITEM_NONE)

  self.hitsmax = 5
  self.str = 10
  self.dex = 15
  self.int = 10

  self.hits = self.hitsmax
  self.defedge = 2
  self.defbash = 2
  self.defice = 1
  self.deffire = 0
  self.defacid = 1
  self.defvolt = 1
  self.eqspec1 = ITEM_JUMP


  ModelSetup()

//-----------------------------------------------------------------------

