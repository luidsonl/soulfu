#define BIG_SCALE 1.25

//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.ButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self, subtype, direction
  float angle

  self = FindSelf()
  subtype = self.eqcol01>>6
  STANDARD.DamageSoundEvent("FILE:-LIZZ01.RAW", "FILE:-LIZZ02.RAW", "FILE:-LIZZ03.RAW")
  if(self.event == EVENT_THIRDIARY_TIMER)
    if(subtype == 3)
      if(self.pttimer > 0)
        RestoreCharacterHits(self, 1, 1)
        if(self.hits < (self.hitsmax>>1))
          self.pttimer = 70
          self.tetimer = 60
        return 0
  if(self.event == EVENT_DAMAGED)
    if(subtype == 1)
      if(!(self.eflags & ENCHANT_FLAG_INVISIBLE))
        self.alpha = 255
    if(subtype == 3)
      if(self.hits < (self.hitsmax>>1))
        self.pttimer = 70
        self.tetimer = 60
        self.combo = 0
    if(self.daction != ACTION_KNOCK_OUT && self.pttimer == 0)
      direction = SystemGet(SYS_GLOBALATTACKSPIN, 0, 0)
      angle = (direction*360)/65536.0
      self.vx = sin(angle+90.0)*0.25
      self.vy = sin(angle)*0.25
      self.spin = direction+32768
      self.vz = 0.50
      

  STANDARD.Event()
  STANDARD.LevelUpEvent(4, 0, 4, 2, 0)


//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype


  // Funky shield stuff...
  FUNKY.ShieldUnpress()
  ModelClear()


  self = FindSelf()
  subtype = (self.eqcol01>>6)
  ModelAssign(self+MODEL_BASE_FILE, "FILE:LIZZANIM.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

  if(subtype == 0)
    // Lizzerman
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:LIZZBODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:DYELLOW.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:SHADLIZZ.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=ARMOR.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)

    ModelAssign(self+MODEL_HEAD_FILE, "FILE:LIZZHEAD.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:SHADLIZZ.RGB")
    STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
    ModelAssign(self+MODEL_HEAD_COLOR, YELLOW)
  if(subtype == 1)
    // Stalker
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:STLKBODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:BLACK.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:BLACK.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:SHADLIZZ.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=ARMOR.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)

    ModelAssign(self+MODEL_HEAD_FILE, "FILE:STLKHEAD.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:BLACK.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:DWHITE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=BAT.RGB")
    STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
    ModelAssign(self+MODEL_HEAD_COLOR, GREEN)
  if(subtype == 2)
    // Naga
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:NAGABODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:STEEL.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:SHADLIZZ.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=ARMOR.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)

    ModelAssign(self+MODEL_HEAD_FILE, "FILE:NAGAHEAD.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:DRED.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=BAT.RGB")
    STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
    ModelAssign(self+MODEL_HEAD_COLOR, YELLOW)
  if(subtype == 3)
    // Dragonman
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:LIZZBODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:RED.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:BRONZE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:SHADLIZZ.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=ARMOR.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)

    ModelAssign(self+MODEL_HEAD_FILE, "FILE:DMANHEAD.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:RED.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:YELLOW.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:SHADLIZZ.RGB")
    STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
    ModelAssign(self+MODEL_HEAD_COLOR, WHITE)



  // !!!BAD!!!
  // !!!BAD!!!  Trident
  // !!!BAD!!!
//  if(subtype == 0 || subtype == 2)
//    ModelAssign(self+MODEL_LEFT2_FILE, "FILE:TRIDENT.RDY")
//    ModelAssign(self+MODEL_LEFT2_TEX0, "FILE:DYELLOW.RGB")
//    ModelAssign(self+MODEL_LEFT2_TEX1, "FILE:STEEL.RGB")
//    ModelAssign(self+MODEL_LEFT2_TEX2, "FILE:WHITE.RGB")
//    ModelAssign(self+MODEL_LEFT2_TEX3, "FILE:WHITE.RGB")
//    ModelAssign(self+MODEL_LEFT2_COLOR, WHITE)
  // !!!BAD!!!
  // !!!BAD!!!
  // !!!BAD!!!



  STANDARD.ItemModelSetup(MODEL_LEFT_FILE)
  STANDARD.ItemModelSetup(MODEL_RIGHT_FILE)



  // Special hatchet & shield recoloring
  if(subtype == 0)
    if((self.eqleft & 248) == ITEM_BROAD_AXE)
      ModelAssign(self+MODEL_LEFT_TEX3, "FILE:PURPLE.RGB")
    if((self.eqright & 248) == ITEM_BROAD_AXE)
      ModelAssign(self+MODEL_RIGHT_TEX3, "FILE:PURPLE.RGB")
  if(subtype == 3)
    if((self.eqleft & 252) == ITEM_SHIELD)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:BRONZE.RGB")
      ModelAssign(self+MODEL_LEFT_TEX2, "FILE:DBRONZE.RGB")
    if((self.eqleft & 252) == ITEM_MIRROR_SHIELD)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:BRONZE.RGB")
      ModelAssign(self+MODEL_LEFT_TEX2, "FILE:DBRONZE.RGB")


//-----------------------------------------------------------------------
int GetName()
  int self, subtype, big

  self = FindSelf()
  subtype = (self.eqcol01>>6)
  big = self.eqcol01&1
  StringClear(NAME_STRING)
  if(big)
    StringAppend(NAME_STRING, "Big ", 256)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Lizzerman", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Stalker", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Naga", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Dragonman", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUsePotions()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  int self
  self = FindSelf()
  self.frontx = self.frontx*BIG_SCALE
  self.fronty = self.fronty*BIG_SCALE
  self.frontz = self.frontz*BIG_SCALE
  self.sidex = self.sidex*BIG_SCALE
  self.sidey = self.sidey*BIG_SCALE
  self.sidez = self.sidez*BIG_SCALE
  self.upx = self.upx*BIG_SCALE
  self.upy = self.upy*BIG_SCALE
  self.upz = self.upz*BIG_SCALE
  self.timer = 1

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype, big
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>6)
  self.eqcol01 = self.eqcol01 & 240 // Clear big flag...
  big = FALSE
  if(subtype != 1)
    if(RollDice(1, 4) == 1)
      big = TRUE
      self.eqcol01 = self.eqcol01 | 1


  self.boxsize = 1.25
  self.height = 6
  self.flags = CHAR_GRAVITY_ON // | CHAR_CAN_RIDE_MOUNT
  self.vflags = VIRTUE_FLAG_EATS_TRIPE
  self.vspin = 700
  self.timer = 0
  self.eqspec1 = ITEM_JUMP


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.5)
  STANDARD.TreasureSetup(difficulty, 10, 1.1, ITEM_HEAL, ITEM_MEAT)
  self.hitsmax = (6*difficulty)+5
  self.str = (10*difficulty)+12
  self.dex = (2*difficulty)+4
  self.int = (4*difficulty)+3


  self.eqright = ITEM_BROAD_AXE
  if(subtype < 2)
    self.defacid = 1
    self.defedge = 2
    self.defbash = 1
    self.defvolt = -2
    self.defice = -2
    self.deffire = -2
    if(subtype == 1)
      self.alpha = 15
      self.eqcol23 = 12<<4 // shield color
      self.eqright = ITEM_DAGGER | WEAPON_VARIANT_POISON
      self.dex = 40
      self.sflags = self.sflags | SCRIPT_NO_POISON
      self.flags = self.flags | CHAR_NO_BACKSTAB
      if(self.item00 != ITEM_NONE)
        self.item00 = ITEM_COOKIE
    else
      self.eqcol23 = 7<<4 // shield color
      self.eqleft = ITEM_SHIELD
    self.vflags = self.vflags | VIRTUE_FLAG_NO_DROWN
  if(subtype == 2)
    self.defacid = 2
    self.defedge = 3
    self.defbash = 2
    self.deffire = -2
    self.defice = 2
    self.defvolt = 3
    self.eqcol23 = 8<<4 // shield color
    self.eqleft = ITEM_SPELL_MISSILE
    self.eqright = ITEM_OBSIDIAN_BLADE | WEAPON_VARIANT_MAGIC
  if(subtype == 3)
    self.defacid = 2
    self.defedge = 5
    self.defbash = 3
    self.defice = -5
    self.deffire = 5
    self.defvolt = 5
    self.eqcol23 = 5<<4 // shield color
    self.eqleft = ITEM_MIRROR_SHIELD
    self.eqright = ITEM_DRAGON_SWORD
    self.vflags = self.vflags | VIRTUE_FLAG_NO_DROWN
  if(big)
    self.boxsize = self.boxsize*BIG_SCALE
    self.height = self.height*BIG_SCALE
    self.timer = 1
    self.mflags = self.mflags | MORE_FLAG_HIT_HEAD
    self.hitsmax = self.hitsmax * 3
    self.vflags = self.vflags | VIRTUE_FLAG_NO_DROWN
  self.hits = self.hitsmax


  STANDARD.FixStats(self)
  ModelSetup()


//-----------------------------------------------------------------------
