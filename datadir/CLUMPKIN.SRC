//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.ButtonEvent()

//-----------------------------------------------------------------------
int Event()
  STANDARD.DamageSoundEvent("FILE:-LUMP01.RAW", "FILE:-LUMP02.RAW", "FILE:-LUMP03.RAW")
  STANDARD.Event()
  STANDARD.LevelUpEvent(4, 0, 3, 3, 0)


//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype, color
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2
  color = (self.eqcol23)&15


  ModelAssign(self+MODEL_BASE_FILE, "FILE:LUMPANIM.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

  if(subtype == 0)
    // Lumpkin
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:LUMPKIN.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, DullColor(color))
  if(subtype == 1)
    // Goblin
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:LUMPKIN.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, DullColor(color))
  if(subtype == 2)
    // Impkin
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:IMPKIN.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DMAGENTA.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:YELLOW.RGB")
  if(subtype == 3)
    // Divilkin
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:IMPKIN.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:RED.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=LUMPKIN.RGB")
  STANDARD.EyeSetup(self+MODEL_CHEST_TEX3, self.team)
  ModelAssign(self+MODEL_CHEST_COLOR, WHITE)
  STANDARD.ItemModelSetup(MODEL_LEFT_FILE)
  STANDARD.ItemModelSetup(MODEL_RIGHT_FILE)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2

  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Lumpkin", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Goblin", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Impkin", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Divilkin", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUsePotions()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2
  self.eqcol23 = RollDice(1, 256)

  self.boxsize = 1.5
  self.height = 4
  self.flags = CHAR_GRAVITY_ON | CHAR_CAN_RIDE_MOUNT
  self.vflags = VIRTUE_FLAG_EATS_TRIPE
  self.vspin = 1024
  self.timer = 1
  self.aitimer = 1

  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 7, 0.4, ITEM_HEAL, ITEM_MEAT)
  self.hitsmax = 5*difficulty
  self.hits = self.hitsmax
  self.str = (4*difficulty)+3
  self.dex = (6*difficulty)+5
  self.int = (3+difficulty)+2


  self.defedge = subtype
  if(subtype == 0)
    if(RollDice(1, 100) < 50)
      self.eqright = ITEM_DAGGER
    else
      self.eqright = ITEM_PICK_AXE
    if(RollDice(1, 100) < 25)
      self.eqleft = ITEM_DAGGER
  if(subtype == 1)
    if(RollDice(1, 100) < 50)
      self.eqright = ITEM_SHORT_SWORD
    else
      self.eqright = ITEM_HATCHET
  if(subtype == 2)
    self.defvolt = -2
    self.defice = 5
    self.deffire = 5
    if(RollDice(1, 100) < 50)
      self.eqright = ITEM_SWORD
    else
      self.eqright = ITEM_DAGGER | WEAPON_VARIANT_POISON
  if(subtype == 3)
    self.defice = -4
    self.deffire = 10
    if(RollDice(1, 100) < 50)
      self.eqright = ITEM_OBSIDIAN_BLADE
    else
      self.eqright = ITEM_BROAD_AXE
    if(RollDice(1, 100) < 50)
      self.eqright = self.eqright | WEAPON_VARIANT_FIRE
  self.eqspec1 = ITEM_JUMP




  STANDARD.FixStats(self)
  ModelSetup()
  STANDARD.PutOnMount()


//-----------------------------------------------------------------------
