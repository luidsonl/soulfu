//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, subtype


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  subtype = self.eqcol01>>6
  event = self.event & 7
  if((self.event & 8))
    ButtonHandled()
    return 0
  else
    if(event == BUTTON_LEFT || event == BUTTON_RIGHT)
      if(!(self.flags & CHAR_GRAVITY_ON))
        // Lurker hasn't fallen yet...
        self.flags = self.flags | CHAR_GRAVITY_ON | CHAR_FALL_ON
        self.setimer = 1
      else
        if(self.action == ACTION_WALK || self.action == ACTION_STAND)
          if(self.flags & CHAR_ON_GROUND)
            // We're on the ground now...  Do spit attacks...
            self.action = ACTION_BASH_LEFT
            self.daction = ACTION_STAND
      ButtonHandled()
      return 0

//-----------------------------------------------------------------------
int Event()
  int self, subtype, oldtarget, target

  self = FindSelf()
  subtype = self.eqcol01>>6
  self.combo = 0
  if(self.event == EVENT_HIT_CHARACTER && (self.flags&CHAR_FALL_ON))
    oldtarget = self.target
    self.target = SystemGet(SYS_COLLISIONCHAR, 0, 0)
    target = FindTarget()
    if(target)
      if(STANDARD.BumpDamageEvent(DAMAGE_EDGE, (subtype<<1)+1, 1, TRUE, TRUE))
        self.spin = self.spin+32768
        if(target.nctimer < 40)
          target.nctimer = 40
        self.rltimer = 5
        SystemSet(SYS_BUMPABORT, 0, 0, 0)
    self.target = oldtarget
    return 0
  if(self.event == EVENT_SECONDARY_TIMER)
    if(self.alpha < 240)
      self.alpha = self.alpha + 16
    if(subtype == 2)
      if(self.alpha < 127)
        self.setimer = 1
    else
      if(self.alpha < 255)
        self.setimer = 1
  if(self.event == EVENT_HIT_FLOOR && self.hits > 0)
    // Hit ground after initial fall...
    self.vz = 0.0
    PDUST.Effect(60)
    SystemSet(SYS_SCREENSHAKE, 0, 2, 15)
  if(self.event == EVENT_DAMAGED && subtype != 3)
    // Cause it to shatter...
    if(self.hits == 0)
      self.pttimer = 100
      PlaySound("FILE:-STATU03.RAW", 256, 64)
  STANDARD.Event()
  STANDARD.LevelUpEvent(10, 0, 0, 0, 0)
  if(self.event == EVENT_DAMAGED)
    if(self.daction == ACTION_STUN)
      self.vx = 0.0
      self.vy = 0.0
    if(self.hits > 0 && self.daction != ACTION_STUN)
      self.action = ACTION_STUN_BEGIN
      self.daction = ACTION_STUN
    if(!(self.flags & CHAR_GRAVITY_ON))
      // Lurker hasn't fallen yet...
      self.flags = self.flags | CHAR_GRAVITY_ON | CHAR_FALL_ON
      self.setimer = 1



//-----------------------------------------------------------------------
int FrameEvent()
  int self, subtype, damage
  float x, y, z, vx, vy, vz


  self = FindSelf()
  subtype = self.eqcol01>>6
  if(self.event & 8)
    if(self.action == ACTION_BASH_LEFT)
      if(subtype == 0)
        damage = RollDice(1, self.str+6) / 6
        PATTACK.Effect(self.x-self.frontx*0.5, self.y-self.fronty*0.5, self.z+1.5, self.frontx*0.2, self.fronty*0.2, 0.0, DAMAGE_EDGE, 2, 1, 2.0, 0.0, 8, 0)
      else
        x = self.x
        y = self.y
        z = self.z+1.5
        AutoAim(0.5, 0.0, self.spin, self.team, self.dex, 8192, AUTOAIM_CRUNCH)
        vx = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_X)
        vy = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Y)
        vz = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Z)
        if(subtype == 1)
          // Mr. Stalagums shoots poisonballs...
          PPSNBALL.ShotEffect(x, y, z, vx, vy, vz, FindIndex(self), 40)
        if(subtype == 2)
          // Frosticle shoots iceballs...
          PICEBALL.ShotEffect(x, y, z, vx, vy, vz, FindIndex(self), 80)
        if(subtype == 3)
          // Iron Spike shoots fireballs...
          PFIRBALL.ShotEffect(x, y, z, vx, vy, vz, FindIndex(self), 120)
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6


  ModelAssign(self+MODEL_BASE_FILE, "FILE:LURKER.RDY")
  if(subtype == 0)
    // Stone Lurker
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DYELLOW.RGB")
  if(subtype == 1)
    // Mr. Stalagums
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DWHITE.RGB")
  if(subtype == 2)
    // Frostcicle
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:GWHITE.RGB")
  if(subtype == 3)
    // Iron Spike
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:STEEL.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:SHADLURK.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=GREMF.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Stone Lurker", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Mr. Stalagums", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Frostcicle", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Iron Spike", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  int self
  self = FindSelf()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()
  self.aitimer = (self.aitimer>>2) + 1

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty


  self = FindSelf()
  subtype = self.eqcol01>>6
  self.boxsize = 1.7
  self.height = 3
  self.sflags = SCRIPT_NO_PETRIFY | SCRIPT_NO_POISON
  self.flags = CHAR_NO_BACKSTAB | CHAR_NO_STUCK_PARTS
  self.vflags = VIRTUE_FLAG_BUMP_IMMUNE
  self.vspin = 100+(subtype*100)
  self.timer = 1
  self.z = self.z + 25.0


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.5)
  STANDARD.TreasureSetup(difficulty, 10, 1.0, ITEM_NONE, ITEM_NONE)
  self.hitsmax = (3*difficulty)+5
  self.hits = self.hitsmax
  self.str = (3*difficulty)+3
  self.dex = 1
  self.int = (2+difficulty)+1
  self.defedge = subtype + 1
  self.defbash = subtype + 1
  if(subtype == 2)
    self.defice = 3
    self.deffire = -3
  if(subtype == 3)
    self.deffire = 2
    self.defice  = 2
    self.defacid = 2
    self.defvolt =-4
  self.alpha = 15


  ModelSetup()

//-----------------------------------------------------------------------
