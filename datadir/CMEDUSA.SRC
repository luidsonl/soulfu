//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.ButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self, subtype

  self = FindSelf()
  subtype = (self.eqcol01>>6)

  STANDARD.Event()
  if(subtype == 2)
    STANDARD.LevelUpEvent(2, 4, 1, 1, 2)
  else
    STANDARD.LevelUpEvent(4, 0, 3, 3, 0)
  STANDARD.JumpOutOfWaterEvent()
  self.eaction = 0

//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  int flesh_color, dress_color, snake_color, eye_color_rgb, model_file
  self = FindSelf()
  subtype = (self.eqcol01>>6)


  // Funky shield stuff...
  FUNKY.ShieldUnpress()


  if(subtype == 0)
    // Kidusa
    model_file = "FILE:PLAYERSM.RDY"
    flesh_color = "FILE:FLESH.RGB"
    snake_color = "FILE:NGREEN.RGB"
    eye_color_rgb = YELLOW
  if(subtype == 1)
    // Gorgon
    model_file = "FILE:PLAYER.RDY"
    flesh_color = "FILE:DGREEN.RGB"
    snake_color = "FILE:GYELLOW.RGB"
    eye_color_rgb = YELLOW
  if(subtype == 2)
    // Medusa
    model_file = "FILE:PLAYER.RDY"
    flesh_color = "FILE:ORANGE.RGB"
    snake_color = "FILE:GPURPLE.RGB"
    eye_color_rgb = YELLOW
  if(subtype == 3)
    // Jivedusa
    model_file = "FILE:PLAYER.RDY"
    flesh_color = "FILE:BLUE.RGB"
    snake_color = "FILE:GWHITE.RGB"
    eye_color_rgb = YELLOW
  dress_color = GlossColor(self.eqcol23&15)



  // Clear the model...
  ModelClear()


  ModelAssign(self+MODEL_BASE_FILE, model_file)
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

  ModelAssign(self+MODEL_HEAD_FILE, "FILE:PLRHEADA.RDY")
  ModelAssign(self+MODEL_HEAD_TEX0, flesh_color)
  ModelAssign(self+MODEL_HEAD_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_HEAD_TEX2, "FILE:DBLACK.RGB")
  ModelAssign(self+MODEL_HEAD_TEX3, "FILE:=EYE1.RGB")
  ModelAssign(self+MODEL_HEAD_COLOR, eye_color_rgb)

  ModelAssign(self+MODEL_EYES_FILE, "FILE:PLRSNAK0.RDY")
  ModelAssign(self+MODEL_EYES_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_EYES_TEX1, snake_color)
  ModelAssign(self+MODEL_EYES_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_EYES_TEX3, "FILE:=EYE1.RGB")
  ModelAssign(self+MODEL_EYES_COLOR, eye_color_rgb)

  ModelAssign(self+MODEL_MOUTH_FILE, "FILE:PLRSNAK1.RDY")
  ModelAssign(self+MODEL_MOUTH_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_MOUTH_TEX1, snake_color)
  ModelAssign(self+MODEL_MOUTH_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_MOUTH_TEX3, "FILE:=EYE1.RGB")
  ModelAssign(self+MODEL_MOUTH_COLOR, eye_color_rgb)

  ModelAssign(self+MODEL_CHEST_FILE, "FILE:PLRTUNI4.RDY")
  ModelAssign(self+MODEL_CHEST_TEX0, flesh_color)
  ModelAssign(self+MODEL_CHEST_TEX1, dress_color)
  ModelAssign(self+MODEL_CHEST_TEX2, "FILE:SHADPNT4.RGB")
  ModelAssign(self+MODEL_CHEST_TEX3, "FILE:SHADTUN2.RGB")
  ModelAssign(self+MODEL_CHEST_COLOR, WHITE)

  ModelAssign(self+MODEL_FEET_FILE, "FILE:PLRFEET1.RDY")
  ModelAssign(self+MODEL_FEET_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_FEET_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_FEET_TEX2, flesh_color)
  ModelAssign(self+MODEL_FEET_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_FEET_COLOR, WHITE)

  ModelAssign(self+MODEL_PADS_FILE, "FILE:PLREARS0.RDY")
  ModelAssign(self+MODEL_PADS_TEX0, flesh_color)
  ModelAssign(self+MODEL_PADS_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_PADS_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_PADS_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_PADS_COLOR, WHITE)


  STANDARD.EyeSetup(self+MODEL_EYES_TEX3, self.team)
  STANDARD.EyeSetup(self+MODEL_MOUTH_TEX3, self.team)
  STANDARD.ItemModelSetup(MODEL_LEFT_FILE)
  STANDARD.ItemModelSetup(MODEL_RIGHT_FILE)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Kidusa", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Gorgon", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Medusa", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Jivedusa", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUsePotions()
  CCARRION.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  int self, index, target, mount, subtype
  float vx, vy, vz, range, distance, dot
  self = FindSelf()


  if(self.pttimer == 0)
    self.eframe = (((self.eframe - 7)+1)%27)+7
    if(SystemGet(SYS_MAINGAMEFRAME, 0, 0) & 1)
      self.mframe = (((self.mframe - 7)+1)%14)+7
    // Find an enemy to petrify
    if(self.alpha > 128 && (SystemGet(SYS_MAINGAMEFRAME, 0, 0) & 7) == 0 && self.daction != ACTION_KNOCK_OUT)
      subtype = (self.eqcol01>>6)
      range = 20.0 + (subtype*10.0)
      range = range*range
      self.spin = self.spin+32767  // Temporary direction change...
      index = 0
      while(index < MAX_CHARACTER)
        target = FindByIndex(index)
        if(target && target != self)
          if(target.team != self.team && target.team != TEAM_NEUTRAL && target.pttimer < 9 && target.daction != ACTION_KNOCK_OUT && target.daction != ACTION_STUN && target.intimer == 0)
            if(!(target.sflags & SCRIPT_NO_PETRIFY))
              // Are we facing the target?
              vx = (target.x - self.x)
              vy = (target.y - self.y)
              vz = (target.z + (target.height-1) - self.z - 4.0)
              dot = self.frontx*vx + self.fronty*vy
              if(dot > 0.0)
                // Is the target facing us?
                dot = target.frontx*vx + target.fronty*vy
                if(dot < 0.0)
                  // Is our target mounted?
                  mount = FindByIndex(target.mount)
                  if(mount)
                    // Aim at mounted character's steed...
                    target = mount


                  // Check the range...
                  distance = (vx*vx + vy*vy + vz*vz)
                  if(distance < range)
                    // Shoot a medusa particle at 'em...
                    vx = (target.x - self.x) * 0.01
                    vy = (target.y - self.y) * 0.01
                    vz = (target.z + (target.height-1) - self.z - 4.0) * 0.01
                    PMEDUSA.Effect(vx, vy, vz, 4.0, FindIndex(self), index)
        index++
      self.spin = self.spin+32767  // Temporary direction change...


  self.timer = 1

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>6)
  self.eqcol23 = RollDice(1, 256)


  self.boxsize = 1.5
  self.height = 4
  self.flags = CHAR_GRAVITY_ON | CHAR_CAN_RIDE_MOUNT
  self.oflags = OTHER_FLAG_NO_MORPH
  self.vspin = 1000
  self.timer = 1
  self.aitimer = 1
  self.tetimer = 1

  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 40, 1.0, ITEM_SUPER_HEAL, ITEM_SUPER_MANA)
  self.hitsmax = 5*difficulty
  self.hits = self.hitsmax
  self.str = (4*difficulty)+3
  self.dex = (6*difficulty)+5
  self.int = 25 + (1.5+difficulty)


  self.defbash = 1
  self.defedge = -3



  self.eqspec1 = ITEM_JUMP
  if(subtype == 0)
    self.money = RollDice(1, 30) + 100
    self.eqleft = ITEM_BOW
    self.eqright = ITEM_DAGGER
    self.item00 = ITEM_QUIVER_10
    self.item01 = ITEM_QUIVER_10
  if(subtype == 1)
    self.money = RollDice(1, 50) + 200
    if(self.str < 20)
      self.str = 20
    self.eqleft = ITEM_LONG_BOW
    self.eqright = ITEM_DAGGER
    self.item00 = ITEM_QUIVER_10
    self.item01 = ITEM_QUIVER_10
    self.item02 = ITEM_QUIVER_10
    self.item03 = ITEM_QUIVER_10
  if(subtype == 2)
    self.money = RollDice(1, 70) + 300
    self.manamax = 30
    self.mana = self.manamax
// !!!BAD!!!
// !!!BAD!!! Should have Mana blast or something...
// !!!BAD!!!
    self.eqleft = ITEM_SPELL_MISSILE
    self.eqright = ITEM_MACE
    self.eqspec2 = ITEM_SPELL_MISSILE
    self.defice = 1
    self.deffire = 1
  if(subtype == 3)
    self.money = RollDice(1, 90) + 400
    if(self.str < 30)
      self.str = 30
    self.eqleft = ITEM_LONG_BOW | WEAPON_VARIANT_FIRE
    self.eqright = ITEM_WAR_HAMMER
    self.defice = 2
    self.deffire = 2
    self.item00 = ITEM_QUIVER_10
    self.item01 = ITEM_QUIVER_10
    self.item02 = ITEM_QUIVER_10
    self.item03 = ITEM_QUIVER_10
    self.item04 = ITEM_QUIVER_10
    self.item05 = ITEM_QUIVER_10

  STANDARD.FixStats(self)
  ModelSetup()
  STANDARD.PutOnMount()


//-----------------------------------------------------------------------
