//-----------------------------------------------------------------------
int ModelSetup()
  int self, color
  self = FindSelf()
  color = (self.eqcol23)&15

  ModelAssign(self+MODEL_BASE_FILE, "FILE:MUSHI.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, DullColor(color))
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:MUSHI.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=MUSHI.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)
  ModelAssign(self+MODEL_BASE_COLOR, YELLOW)

//-----------------------------------------------------------------------
int GetName()
  int self
  self = FindSelf()
  StringClear(NAME_STRING)
  StringAppend(NAME_STRING, "Mushi", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.RestrictedButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self, target_index, target
  self = FindSelf()


  STANDARD.DamageSoundEvent("FILE:-RAT01.RAW", NULL, NULL)


  if(self.event == EVENT_SECONDARY_TIMER)
    self.flatten = self.timer * self.timer / 3
    if(self.timer < 5)
      // Time to poof...
      self.action = ACTION_KNOCK_OUT
      self.daction = ACTION_KNOCK_OUT
      PDUST.Effect(30)
      self.z = self.z+0.5
      STANDARD.GiveExperience()
      STANDARD.DeathEffect()
      GoPoof(POOF_SELF)
    self.setimer = 1
  if(self.event == EVENT_HIT_CHARACTER)
    target_index = SystemGet(SYS_COLLISIONCHAR, 0, 0)
    target = FindByIndex(target_index)
    if(target)
      if(target.z > (self.z + 1.0) && target.vz < 0.0 && target.team != self.team && !(target.flags & CHAR_HOVER_ON))
        // Bumper is jumping on our head...
        self.setimer = 1
        self.rltimer = 50
        self.timer = 25
        target.vz = 0.50
        self.nctimer = 255
        self.action = ACTION_STUN_BEGIN
        self.daction = ACTION_STUN
        self.lasthit = target_index
        PlaySound("FILE:-SQUISH.RAW", 245+RollDice(1, 20), 255)
      else
        STANDARD.BumpDamageEvent(DAMAGE_EDGE, 1, 1, FALSE, TRUE)
  if(self.event == EVENT_DAMAGED)
    self.combo++
  STANDARD.Event()
  STANDARD.LevelUpEvent(6, 0, 0, 4, 0)


//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self
  self = FindSelf()
  self.eqcol23 = RollDice(1, 256)


  self.boxsize = 1.3
  self.height = 2
  self.flags = CHAR_GRAVITY_ON | CHAR_FINISH_ACTION
  self.sflags = SCRIPT_BUMP_DAMAGE
  self.vflags = VIRTUE_FLAG_RUN_ME_OVER
  self.oflags = OTHER_FLAG_EASY_ENTANGLE
  self.vspin = 1024
  self.timer = 1

  self.hitsmax = 4 + RollDice(1, 3)
  self.hits = self.hitsmax
  self.str = RollDice(1, 6) + 6
  self.dex = RollDice(1, 6) + 9
  self.int = RollDice(1, 3) + 2
  self.expgive = self.hitsmax
  self.defedge = 0
  self.defbash = -1

  ModelSetup()

//-----------------------------------------------------------------------
