#define STATE_NORMAL   0
#define STATE_HIT_WALL 1
#define BIG_SCALE 1.25

//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.ButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self, subtype, helm, target, pitch
  float x, y, dot
  self = FindSelf()



  STANDARD.DamageSoundEvent("FILE:-PORC01.RAW", "FILE:-PORC02.RAW", "FILE:-PORC03.RAW")


  // Handle wall bumps for AI...
  if(self.aitimer > 0 && self.event == EVENT_HIT_WALL)
    self.aistate = STATE_HIT_WALL
    self.aitimer = 1
    self.cdtimer = 130 // Extra long delay until we get another hit wall event...
    return TRUE


  // Handle waypoint hits for the AI...
  if(self.aitimer > 0 && self.event == EVENT_HIT_WAYPOINT)
    if(self.daction != ACTION_BLOCK)
      self.aitimer = 1
      return TRUE


  // Handle friendly bumps for AI...
  if(self.aitimer > 0 && self.event == EVENT_HIT_CHARACTER)
    target = FindByIndex(SystemGet(SYS_COLLISIONCHAR, 0, 0))
    if(target)
      if(self.action == ACTION_WALK)
        if(target.team == self.team || target.team == TEAM_NEUTRAL)
          // We're on the same team
          if(GetDirection(target.x, target.y) == DIRECTION_FRONT)
            // We're facing whoever we bumped...  Let's change course a little...
            x = target.x - self.x
            y = target.y - self.y
            dot = (self.sidex*x) + (self.sidey*y)
            if(dot > 0.0)
              self.gotox = self.x + (self.sidex*5.0)
              self.gotoy = self.y + (self.sidey*5.0)
            else
              self.gotox = self.x + (self.sidex*5.0)
              self.gotoy = self.y + (self.sidey*5.0)
            self.aitimer = 10 + RollDice(1, 8)
        else
          // We've bumped an enemy...
          self.aitimer = 1
          if(GetDirection(target.x, target.y) == DIRECTION_FRONT)
            self.target = SystemGet(SYS_COLLISIONCHAR, 0, 0)
    STANDARD.Event()
    return TRUE


  // Normal block routine...
  if(self.event == EVENT_BLOCKED)
    STANDARD.Event()

    // Handle blocked attacks for AI...
    if(self.aitimer > 0)
      target = FindTarget()
      if(target)
        // Target is valid...
        if(target.action >= ACTION_BASH_LEFT && target.action <= ACTION_SLASH_RIGHT)
          // Looks like our target was probably attacking us...  Try counter attack...
          ButtonPress(BUTTON_UNPRESS, BUTTON_LEFT, 0)
          self.gotox = target.x+(self.sidex*2.0)
          self.gotoy = target.y+(self.sidey*2.0)
          self.action = ACTION_BASH_RIGHT
          self.daction = ACTION_STAND
          self.aitimer = 25 + RollDice(1, 10)
        else
          // We're getting shot by arrows (or hit by someone else...
          // Lets keep that shield up a bit longer...
          self.aitimer = 160 + RollDice(1, 160)
      return TRUE


  // Handle texture changes for petrify, unpetrify...
  if(self.event == EVENT_PETRIFIED || self.event == EVENT_UNPETRIFIED)
    ModelSetup()
    return TRUE


  // Handle taking damage for the AI...
  if(self.aitimer > 0 && self.event == EVENT_DAMAGED)
    target = FindTarget()
    if(!target)
      // We don't have a valid target
      self.target = SystemGet(SYS_GLOBALATTACKER, 0, 0)
      self.aitimer = 15


  // Default events...
  STANDARD.Event()
  STANDARD.LevelUpEvent(6, 0, 2, 2, 0)

//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype, helm
  self = FindSelf()
  subtype = (self.eqcol01>>6)
  helm = self.team != TEAM_GOOD


  // Funky shield stuff...
  FUNKY.ShieldUnpress()


  // Animation file...
  ModelAssign(self+MODEL_BASE_FILE, "FILE:PORCANIM.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)


  // Funny petrification thing...
  if(self.pttimer > 0)
    subtype = 4


  // Model file...
  if(subtype == 0)
    // Porc
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:OINKBODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:STEEL.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:MPPORC0.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=PORC0.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)
    if(helm)
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:OINKHEAD.RDY")
      ModelAssign(self+MODEL_HEAD_TEX0, "FILE:STEEL.RGB")
      ModelAssign(self+MODEL_HEAD_TEX1, "FILE:MPPORC0.RGB")
      ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=PORC0.RGB")
      ModelAssign(self+MODEL_HEAD_TEX3, "FILE:=EYE1.RGB")
      ModelAssign(self+MODEL_HEAD_COLOR, DARK_RED)
    else
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:PORCHEAD.RDY")
      ModelAssign(self+MODEL_HEAD_TEX0, "FILE:FLESH.RGB")
      ModelAssign(self+MODEL_HEAD_TEX1, "FILE:MPPORC0.RGB")
      ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=PORCF0.RGB")
      STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
      ModelAssign(self+MODEL_HEAD_COLOR, WHITE)
  if(subtype == 1)
    // O'ink-Hai
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:OINKBODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:BRONZE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:MPPORC1.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=PORC1.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)
    if(helm)
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:OINKHEAD.RDY")
      ModelAssign(self+MODEL_HEAD_TEX0, "FILE:BRONZE.RGB")
      ModelAssign(self+MODEL_HEAD_TEX1, "FILE:MPPORC1.RGB")
      ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=PORC1.RGB")
      ModelAssign(self+MODEL_HEAD_TEX3, "FILE:=EYE1.RGB")
      ModelAssign(self+MODEL_HEAD_COLOR, GREEN)
    else
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:PORCHEAD.RDY")
      ModelAssign(self+MODEL_HEAD_TEX0, "FILE:BLUE.RGB")
      ModelAssign(self+MODEL_HEAD_TEX1, "FILE:MPPORC1.RGB")
      ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=PORCF0.RGB")
      STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
      ModelAssign(self+MODEL_HEAD_COLOR, WHITE)
  if(subtype == 2 || subtype == 3)
    // Dark Champion
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:OINKBODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DSTEEL.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:MPPORC2.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=PORC2.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)
    if(helm)
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:OINKHEAD.RDY")
      ModelAssign(self+MODEL_HEAD_TEX0, "FILE:DSTEEL.RGB")
      ModelAssign(self+MODEL_HEAD_TEX1, "FILE:MPPORC2.RGB")
      ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=PORC2.RGB")
      ModelAssign(self+MODEL_HEAD_TEX3, "FILE:=EYE1.RGB")
      ModelAssign(self+MODEL_HEAD_COLOR, YELLOW)
    else
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:PORCHEAD.RDY")
      ModelAssign(self+MODEL_HEAD_TEX0, "FILE:DMAGENTA.RGB")
      ModelAssign(self+MODEL_HEAD_TEX1, "FILE:MPPORC1.RGB")
      ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=PORCF0.RGB")
      STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
      ModelAssign(self+MODEL_HEAD_COLOR, WHITE)
  if(subtype == 4)
    // Petrified porc...
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:OINKBODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DWHITE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:MPPORCP.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=PORCP.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:DWHITE.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)
    if(helm)
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:OINKHEAD.RDY")
      ModelAssign(self+MODEL_HEAD_TEX0, "FILE:DWHITE.RGB")
      ModelAssign(self+MODEL_HEAD_TEX1, "FILE:MPPORCP.RGB")
      ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=PORCP.RGB")
      ModelAssign(self+MODEL_HEAD_TEX3, "FILE:=EYE1.RGB")
      ModelAssign(self+MODEL_HEAD_COLOR, WHITE)
    else
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:PORCHEAD.RDY")
      ModelAssign(self+MODEL_HEAD_TEX0, "FILE:DWHITE.RGB")
      ModelAssign(self+MODEL_HEAD_TEX1, "FILE:MPPORCP.RGB")
      ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=PORCF0.RGB")
      STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
      ModelAssign(self+MODEL_HEAD_COLOR, WHITE)
  STANDARD.ItemModelSetup(MODEL_LEFT_FILE)
  STANDARD.ItemModelSetup(MODEL_RIGHT_FILE)


  // Special mirror shield & war hammer recoloring...
  if(subtype == 1)
    if((self.eqleft & 252) == ITEM_SHIELD)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:BRONZE.RGB")
      ModelAssign(self+MODEL_LEFT_TEX2, "FILE:DBRONZE.RGB")
    if((self.eqleft & 252) == ITEM_MIRROR_SHIELD)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:BRONZE.RGB")
      ModelAssign(self+MODEL_LEFT_TEX2, "FILE:DBRONZE.RGB")
  if(subtype == 2 || subtype == 3)
    if((self.eqleft & 252) == ITEM_MIRROR_SHIELD)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:DSTEEL.RGB")
      ModelAssign(self+MODEL_LEFT_TEX2, "FILE:DSTEEL.RGB")
    if((self.eqleft & 248) == ITEM_WAR_HAMMER)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:DSTEEL.RGB")
      ModelAssign(self+MODEL_LEFT_TEX1, "FILE:=WARHAMP.RGB")
    if((self.eqright & 248) == ITEM_WAR_HAMMER)
      ModelAssign(self+MODEL_RIGHT_TEX0, "FILE:DSTEEL.RGB")
      ModelAssign(self+MODEL_RIGHT_TEX1, "FILE:=WARHAMP.RGB")


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Porc", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "O'ink-Hai", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Dark Warrior", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Dark Champion", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  int self, target, oldtarget, state, chase_enemy, temp
  float x, y, angle
  float distance, dot, check
  self = FindSelf()


  // Twiddle some stuff...
  state = self.aistate
  self.aistate = STATE_NORMAL
  if(self.daction == ACTION_BLOCK)
    ButtonPress(BUTTON_UNPRESS, BUTTON_LEFT, 0)


  // Look around for an enemy to fight...
  oldtarget = self.target
  self.target = AcquireTarget(ACQUIRE_SEE_ENEMY, NULL, 75.0)
  target = FindTarget()
  if(target)
    // We had an enemy in front of us, but can we see 'em?
    if(!LineOfSight(target.x, target.y, target.z+target.height))
      // Can't see 'em...  Revert to old target...
      target = NULL


  if(!target)
    // Didn't see anybody in front of us...
    self.target = oldtarget
    target = FindTarget()
    if(target)
      // Old target is still valid...  Only lose target if it gets too far away (20ft)...
      x = target.x - self.x
      y = target.y - self.y
      distance = (x*x) + (y*y)
//      if(target.alpha <= 100)
//        // Harder to keep track of invisible guys
//        distance = distance * 4.0
      if(distance > 400.0)
        target = NULL
        self.target = 65535
    else
      // Old target isn't valid either...
      self.target = 65535





  // Find a path to our target (if there is a path to 'em)...
  x = self.gotox
  y = self.gotoy
  chase_enemy = FALSE
  if(target)
    self.gotox = target.x
    self.gotoy = target.y
    chase_enemy = FindPath()



  // Are we chasing an enemy, or just wandering around?
  if(chase_enemy)
    // Are we facing our target?
    oldtarget = FindByIndex(oldtarget)
    if(GetDirection(target.x, target.y) == DIRECTION_FRONT)
      // Yup...  Give us an exclamation if newly acquired...
      if(!oldtarget && oldtarget != target)
        if(self.action == ACTION_STAND || self.action == ACTION_WALK || self.action == ACTION_SPECIAL_3)
          PEXCLAIM.Effect(self.x, self.y, self.z+5.0, self.boxsize, FALSE)
          self.action = ACTION_SPECIAL_4
          PlaySound("FILE:-PORC00.RAW", 256, 255)



      // Are we anywhere near our target?
      x = self.x - target.x
      y = self.y - target.y
      distance = x*x + y*y
      check = self.boxsize + target.boxsize + 10.0
      if(target.eqleft == ITEM_BOW || target.eqright == ITEM_GONNE || target.class == 0 || target.class == CLASS_WIZARD || target.class == CLASS_MYSTIC)
        check = check + 12.0
      if(distance < (check*check))
        // We're kinda gettin' there...  Close enough to block anyway...
        dot = (target.frontx*x) + (target.fronty*y)
        if(dot > 0.00 && RollDice(1, 100) > 75 && target.daction != ACTION_BLOCK)
          // Lets try blocking...
          self.aitimer = 150 + RollDice(1, 250)
          ButtonPress(BUTTON_PRESS_AND_HOLD, BUTTON_LEFT, 0)
          return TRUE
        else
          // Are we close enough to attack?
          check = self.boxsize + target.boxsize + 3.0
          if(distance < (check*check))
            // Close enough to strike...
            self.gotox = target.x+(self.sidex*2.0)
            self.gotoy = target.y+(self.sidey*2.0)
            ButtonPress(BUTTON_PRESS, BUTTON_RIGHT, 0)
            self.aitimer = 25 + RollDice(1, 10)
            return TRUE
    else
      // Funky thing so exclamation doesn't happen until we turn to face our target...
      if(!oldtarget && oldtarget != target)
        self.target = 65535
    self.aitimer = 10 + RollDice(1, 10)
  else
    if(state == STATE_HIT_WALL)
      // Stop moving...
      self.gotox = self.x
      self.gotoy = self.y
    else
      // Restore in case targeting screwed it up...
      self.gotox = x
      self.gotoy = y
    self.aitimer = 20 + RollDice(1, 20)



    x = self.gotox - self.x
    y = self.gotoy - self.y
    x = x*x + y*y
    if(x < 4.0)
      // Pretty close to waypoint...  Let's start going to another...
      if(self.action == ACTION_STAND || self.action == ACTION_WALK)
        // Look around before proceeding to next waypoint...
        self.action = ACTION_SPECIAL_3
        self.daction = ACTION_STAND
        temp = 66 - self.dex
        if(temp < 20)
          temp = 20
        self.aitimer = temp
      else
        angle = RollDice(1, 256) * 1.40625
        x = sin(angle)*30.0
        y = sin(angle+90.0)*30.0
        self.gotox = self.x + x
        self.gotoy = self.y + y
        FindPath()



//-----------------------------------------------------------------------
int Refresh()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>6)
  if(subtype == 3)
    self.frontx = self.frontx*BIG_SCALE
    self.fronty = self.fronty*BIG_SCALE
    self.frontz = self.frontz*BIG_SCALE
    self.sidex = self.sidex*BIG_SCALE
    self.sidey = self.sidey*BIG_SCALE
    self.sidez = self.sidez*BIG_SCALE
    self.upx = self.upx*BIG_SCALE
    self.upy = self.upy*BIG_SCALE
    self.upz = self.upz*BIG_SCALE
    self.timer = 1
  else
    STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  self.boxsize = 2.0
  self.height = 6
  self.flags = CHAR_GRAVITY_ON | CHAR_CAN_RIDE_MOUNT
  self.vflags = VIRTUE_FLAG_EATS_TRIPE
  self.vspin = 640 + (subtype << 7)
  self.timer = 1
  self.aitimer = 1
  self.aistate = STATE_NORMAL


  self.eqleft = ITEM_SHIELD
  if(subtype == 0)
    self.eqright = ITEM_SWORD
    self.eqcol23 = 8<<4 // shield color
  if(subtype == 1)
    self.eqright = ITEM_MACE
    self.eqcol23 = 2<<4 // shield color
  if(subtype == 2)
    self.eqright = ITEM_WAR_HAMMER
    self.eqcol23 = 5<<4 // shield color
    self.eqleft = ITEM_MIRROR_SHIELD
  if(subtype == 3)
    self.eqright = ITEM_DRAGON_SWORD | WEAPON_VARIANT_VOLT
    self.eqcol23 = 5<<4 // shield color
    self.eqleft = ITEM_MIRROR_SHIELD
    self.boxsize = self.boxsize*BIG_SCALE
    self.height = self.height*BIG_SCALE
    self.flags = self.flags & (65535-CHAR_CAN_RIDE_MOUNT)
    self.vflags = self.vflags & (255-VIRTUE_FLAG_EATS_TRIPE)
  self.eqspec1 = ITEM_JUMP




  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 10, 1.0, ITEM_PORC, ITEM_PORC)
  self.hitsmax = 10*difficulty
  self.hits = self.hitsmax
  self.str = (7*difficulty)+14
  self.dex = (6*difficulty)+10
  self.int = 6+difficulty
  self.defedge = 3+difficulty
  self.defbash = 1+difficulty
  self.defvolt = 2*(subtype+2)
  self.deffire = subtype+1
  self.defice = 0

  STANDARD.FixStats(self)
  ModelSetup()
  STANDARD.PutOnMount()

//-----------------------------------------------------------------------
