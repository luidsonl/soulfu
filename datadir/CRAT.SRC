//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.RestrictedButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2
  STANDARD.DamageSoundEvent("FILE:-RAT01.RAW", NULL, NULL)
  if(subtype == 3)
    STANDARD.BumpDamageEvent(DAMAGE_ICE, 3, 1, FALSE, TRUE)
  else
    if(STANDARD.BumpDamageEvent(DAMAGE_EDGE, subtype, 1, FALSE, TRUE))
      if(subtype == 2)
        STANDARD.PoisonCharacter(SystemGet(SYS_COLLISIONCHAR, 0, 0), FindIndex(self), 600)
  STANDARD.Event()
  STANDARD.LevelUpEvent(6, 0, 0, 4, 0)

//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2


  ModelAssign(self+MODEL_BASE_FILE, "FILE:RAT.RDY")
  if(subtype == 0)
    // Great rat
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DYELLOW.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DRED.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)
  if(subtype == 1)
    // Lab rat
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DWHITE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DPURPLE.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)
  if(subtype == 2)
    // Plague rat
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DPURPLE.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, YELLOW)
  if(subtype == 3)
    // Snow King rat
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=RAT.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2

  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Great Rat", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Lab Rat", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Plague Rat", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Snow King Rat", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2

  self.boxsize = 1.2
  self.height = 3
  self.flags = CHAR_GRAVITY_ON
  self.sflags = SCRIPT_NO_KNOCK_OUT | SCRIPT_BUMP_DAMAGE
  self.vflags = VIRTUE_FLAG_RUN_ME_OVER | VIRTUE_FLAG_EATS_TRIPE
  self.oflags = OTHER_FLAG_EASY_ENTANGLE
  self.vspin = 1024
  self.timer = 1

  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  if(subtype == 2)
    // Plague rats have rotten meat...
    STANDARD.TreasureSetup(difficulty, 5, 0.0, ITEM_NONE, ITEM_MEAT_ROTTEN)
  else
    STANDARD.TreasureSetup(difficulty, 5, 0.0, ITEM_NONE, ITEM_MEAT)
  self.hitsmax = 3*difficulty+1
  self.hits = self.hitsmax
  self.str = 3*difficulty
  self.dex = 7*difficulty
  self.int = 2*difficulty+2
  self.defedge = subtype
  self.defbash = subtype>>1
  self.defice = subtype
  if(subtype == 2)
    self.defacid = 2
    self.sflags = self.sflags | SCRIPT_NO_POISON
  if(subtype == 3)
    self.deffire = -2
    self.defice = 6



  ModelSetup()

//-----------------------------------------------------------------------
