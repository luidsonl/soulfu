//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event
  float floorz


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  event = self.event & 7
  if((self.event & 8))
    ButtonHandled()
    return 0
  else
    if(event == BUTTON_LEFT || event == BUTTON_RIGHT)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        if(self.rrtimer == 0)
          self.action = ACTION_SLASH_RIGHT
          self.daction = ACTION_STAND
          ButtonHandled()
          if(self.dex < 60)
            self.rrtimer = 60 - self.dex
        return 0
    if(self.daction != ACTION_KNOCK_OUT)
      if(self.event == BUTTON_SPECIAL1)
        // Change level...
        floorz = RoomWaterLevel()
        if(floorz < -10.0)
          floorz = -10.0
        if(floorz < self.floorz)
          floorz = self.floorz
        if(self.hoverz < floorz + 4.0)
          // We're low, so let's go up...
          self.hoverz = 20.0
        else
          // We're high, so let's go down...
          PlaySound("FILE:-RATTLE.RAW", 256, 255)
          self.hoverz = floorz + 2.0
        self.tetimer = 1
        ButtonHandled()
        return 0

//-----------------------------------------------------------------------
int Event()
  int self, petrify, alpha
  float floorz


  self = FindSelf()
  petrify = self.pttimer
  STANDARD.DamageSoundEvent("FILE:-BUGHIT.RAW", NULL, NULL)
  if(self.event == EVENT_DAMAGED)
    if(petrify)
      self.hits = 0
  if(self.event == EVENT_TIMER)
    if(self.daction == ACTION_KNOCK_OUT)
      self.flags = (self.flags & (65535-CHAR_HOVER_ON)) | CHAR_GRAVITY_ON
      return 0
  if(self.event == EVENT_THIRDIARY_TIMER)
    if(!(self.eflags & ENCHANT_FLAG_INVISIBLE))
      floorz = RoomWaterLevel()
      if(floorz < self.floorz)
        floorz = self.floorz
      if(self.hoverz < 19.0)
        if(self.alpha < 255)
          alpha = self.alpha + 10
          if(alpha < 255)
            self.alpha = alpha
          else
            self.alpha = 255
          self.tetimer = 1
      else
        if(self.alpha > 64)
          alpha = self.alpha - 10
          if(alpha > 64)
            self.alpha = alpha
          else
            self.alpha = 64
          self.tetimer = 1
  STANDARD.Event()
  STANDARD.LevelUpEvent(6, 0, 0, 4, 0)
  if(self.action == ACTION_KNOCK_OUT_BEGIN)
    self.evtimer = 20
    self.hoverz = self.z + 20.0
    self.pttimer = petrify
  if(self.action == ACTION_STUN_BEGIN || self.action == ACTION_KNOCK_OUT_BEGIN)
    self.vx = self.vx * 0.25
    self.vy = self.vy * 0.25

//-----------------------------------------------------------------------
int FrameEvent()
  int self, damage, special_flags, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6


  if(self.event & 8)
    damage = RollDice(1, self.str+6) / 6
    special_flags = SPECIAL_FLAG_KNOCK_DOWN
    if(subtype == 1)
      special_flags = special_flags | SPECIAL_FLAG_POISON
    PATTACK.Effect(self.x+self.frontx*0.5, self.y+self.fronty*0.5, self.z+1.0, self.frontx*0.6, self.fronty*0.6, 0.0, DAMAGE_EDGE, damage, 1, 4.0, 0.0, 5, special_flags)
    PlaySound("FILE:SWIPE.RAW", 225 + RollDice(1, 50), 255)

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6


  ModelAssign(self+MODEL_BASE_FILE, "FILE:ROACHIE.RDY")
  if(subtype == 0)
    // Roachie
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:NDEEPBLU.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DMAGENTA.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)
  if(subtype == 1)
    // Slippergoo
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:NRED.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, YELLOW)
  if(subtype == 2)
    // Snatcher
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DRED.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, YELLOW)
  if(subtype == 3)
    // Grippler
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:GBLACK.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:DBLUE.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=GRUBMUM.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)

//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6

  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Roachie", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Slippergoo", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Snatcher", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Grippler", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  int target, self, subtype
  float x, y, distance, max_distance
  STANDARD.AIUseTripe()
  STANDARD.AIScript()
  self = FindSelf()
  subtype = self.eqcol01>>6
  target = FindTarget()
  if(target)
    if(target.team != self.team)
      x = target.x - self.x
      y = target.y - self.y
      distance = x*x + y*y
      if(distance < 49.0)
        // Descend if near...
        if(self.hoverz > (self.floorz+4.0))
          ButtonPress(BUTTON_PRESS, BUTTON_SPECIAL1, 0)
      else
        max_distance = 20.0 - ((subtype+1)*2.5)
        max_distance = max_distance*max_distance
        if(distance > max_distance)
          // Ascend if far...
          if(self.hoverz < (self.floorz+4.0))
            ButtonPress(BUTTON_PRESS, BUTTON_SPECIAL1, 0)
    else
      // Ascend if target is on same team...
      if(self.hoverz < (self.floorz+4.0))
        ButtonPress(BUTTON_PRESS, BUTTON_SPECIAL1, 0)
  else
    // Ascend if no target...
    if(self.hoverz < (self.floorz+4.0))
      ButtonPress(BUTTON_PRESS, BUTTON_SPECIAL1, 0)


//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty


  self = FindSelf()
  subtype = self.eqcol01>>6
  self.boxsize = 2.0
  self.height = 5
  self.flags = CHAR_FINISH_ACTION | CHAR_HOVER_ON
  self.sflags = SCRIPT_NO_KNOCK_OUT
  self.vflags = VIRTUE_FLAG_EATS_TRIPE
  self.timer = 1
  self.tetimer = 1
  self.hoverz = 20.0
  self.z = self.hoverz
  self.alpha = 64


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 15, 0.0, ITEM_NONE, ITEM_COOKIE)
  self.hitsmax = (5*difficulty)+3
  self.hits = self.hitsmax
  self.str = (4*difficulty)+4
  self.dex = (6*difficulty)+4
  self.int = (2+difficulty)+3
  self.defedge = subtype
  self.defbash = -3
  if(subtype == 1)
    self.defacid = 2
    self.sflags = self.sflags | SCRIPT_NO_POISON
  if(subtype == 2)
    self.deffire = 2
  if(subtype == 3)
    self.defice = 2
  self.vspin = 256+(subtype*100)


  ModelSetup()

//-----------------------------------------------------------------------
