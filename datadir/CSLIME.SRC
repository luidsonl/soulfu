//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  ModelAssign(self+MODEL_BASE_FILE, "FILE:SLIME.RDY")

  if(subtype == 0)
    // Slime
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:PBLUE.RGB")
  if(subtype == 1)
    // Ooze
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:PPURPLE.RGB")
  if(subtype == 2)
    // Blob
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:PGREEN.RGB")
  if(subtype == 3)
    // Cement
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:PSTONE.RGB")


  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=BAT.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)
  ModelAssign(self+MODEL_BASE_COLOR, YELLOW)

//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = (self.eqcol01>>6)


  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Slime", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Ooze", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Blob", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Cement", 256)
  STANDARD.NameAppendHits()


//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.RestrictedButtonEvent()


//-----------------------------------------------------------------------
int Event()
  int self, count, child, subtype

  self = FindSelf()
  subtype = (self.eqcol01>>4) >> 2
  STANDARD.DamageSoundEvent("FILE:-SLIME01.RAW", "FILE:-SLIME02.RAW", "FILE:-SLIME03.RAW")
  if(self.event == EVENT_HIT_FLOOR)
    if(self.hits < 1)
      // Split into several slimettes...  Number depends on max hits...
      count = 0
      while(count < (self.hitsmax+5))
        SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TEAM, self.team)
        SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_SUBTYPE, self.eqcol01)
        SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TARGET, self.target)
        child = Spawn(CHARACTER, self.x, self.y, self.z, "FILE:CSLIMETT.RUN")
        if(child)
          child.aitimer = 1+(count<<1)
          child.nctimer = 10
          child.vx = (RollDice(1, 11) - 6) * 0.03
          child.vy = (RollDice(1, 11) - 6) * 0.03
          child.vz = 0.5
        count = count+5

  if(STANDARD.BumpDamageEvent(DAMAGE_ACID, subtype+1, 1, FALSE, TRUE))
    if(subtype == 2)
      STANDARD.PoisonCharacter(SystemGet(SYS_COLLISIONCHAR, 0, 0), FindIndex(self), 600)
    if(subtype == 3)
      STANDARD.PetrifyCharacter(SystemGet(SYS_COLLISIONCHAR, 0, 0), FindIndex(self), 600)
  STANDARD.Event()
  STANDARD.LevelUpEvent(10, 0, 0, 0, 0)
  if(self.event == EVENT_DAMAGED && self.action == ACTION_KNOCK_OUT_BEGIN && self.hits < 1)
    self.intimer = 180

//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()
  // !!!BAD!!!  Spawn goo particles on floor...

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty


  self = FindSelf()
  subtype = (self.eqcol01>>6)
  self.flags = CHAR_GRAVITY_ON | CHAR_FINISH_ACTION | CHAR_NO_BACKSTAB
  self.vflags = VIRTUE_FLAG_RUN_ME_OVER | VIRTUE_FLAG_EATS_TRIPE | VIRTUE_FLAG_NO_DROWN



  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.5)
  STANDARD.TreasureSetup(difficulty, 7, 0.5, ITEM_NONE, ITEM_NONE)
  self.vspin = 1024 + (subtype*200)

  self.hitsmax = (3*difficulty)+5
  self.str = (3*difficulty)+3
  self.dex = (2*difficulty)+4
  self.int = (1*difficulty)+3
  self.boxsize = 1.5
  self.height = 4
  self.sflags = SCRIPT_BUMP_DAMAGE | SCRIPT_NO_POISON

  self.defacid = subtype+2
  self.defedge = subtype
  self.deffire = 1
  self.defvolt = -1-subtype
  self.defice = -1-subtype
  if(subtype == 3)
    self.sflags = self.sflags | SCRIPT_NO_PETRIFY
  self.hits = self.hitsmax


  ModelSetup()

//-----------------------------------------------------------------------
