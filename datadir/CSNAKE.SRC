//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, subtype


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  subtype = self.eqcol01>>6
  event = self.event & 7
  if((self.event & 8))
    ButtonHandled()
    return 0
  else
    if(event == BUTTON_LEFT || event == BUTTON_RIGHT)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        if(subtype == 3)
          // Do spit attacks...
          self.action = ACTION_BASH_LEFT
          self.daction = ACTION_STAND
      ButtonHandled()
      return 0


//-----------------------------------------------------------------------
int Event()
  int self, damage, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6
  damage = RollDice(1, subtype+1)
  if(STANDARD.BumpDamageEvent(DAMAGE_EDGE, damage, 1, FALSE, TRUE))
    if(subtype > 0)
      STANDARD.PoisonCharacter(self.target, FindIndex(self), (self.str<<5) + 200)
  STANDARD.Event()
  STANDARD.LevelUpEvent(4, 0, 2, 4, 0)
  if(self.event == EVENT_DAMAGED)
    self.combo = 0
    if(self.daction == ACTION_STUN)
      self.evtimer = (self.evtimer >> 1) + 1

//-----------------------------------------------------------------------
int FrameEvent()
  int self, subtype, damage
  float x, y, z, vx, vy, vz


  self = FindSelf()
  subtype = self.eqcol01>>6
  if(self.event & 8)
    if(self.action == ACTION_BASH_LEFT)
      if(subtype == 3)
        x = self.x
        y = self.y
        z = self.z+1.5
        AutoAim(0.5, 0.0, self.spin, self.team, self.dex, 8192, AUTOAIM_CRUNCH)
        vx = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_X)
        vy = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Y)
        vz = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Z)
        PPSNBALL.ShotEffect(x, y, z, vx, vy, vz, FindIndex(self), 40)
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6

  ModelAssign(self+MODEL_BASE_FILE, "FILE:SNAKE.RDY")
  if(subtype == 0)
    // Emerald Snake
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)
  if(subtype == 1)
    // Dust Rattler
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DYELLOW.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)
  if(subtype == 2)
    // Twister
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, YELLOW)
  if(subtype == 3)
    // Spit Viper
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:PURPLE.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, GREEN)
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:=RAT.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)

//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Emerald Snake", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Dust Rattler", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Twister", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Spit Viper", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty


  self = FindSelf()
  subtype = self.eqcol01>>6


  self.boxsize = 1.4
  self.height = 3
  self.flags = CHAR_GRAVITY_ON
  self.sflags = SCRIPT_NO_KNOCK_OUT | SCRIPT_BUMP_DAMAGE
  self.vflags = VIRTUE_FLAG_RUN_ME_OVER | VIRTUE_FLAG_EATS_TRIPE
  self.oflags = OTHER_FLAG_EASY_ENTANGLE
  self.timer = 1


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 2, 0.0, ITEM_NONE, ITEM_NONE)
  self.hitsmax = (2*difficulty)+1
  self.hits = self.hitsmax
  self.str = (2*difficulty)+3
  self.dex = (2*difficulty)+1
  self.int = (1+difficulty)+1
  self.defedge = subtype
  self.defbash = -3
  if(subtype == 1)
    self.defice = 2
  if(subtype == 2)
    self.deffire = 2
  if(subtype == 3)
    self.defacid = 2
  self.vspin = 256+(subtype*100)


  ModelSetup()

//-----------------------------------------------------------------------
