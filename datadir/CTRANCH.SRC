#define DUST_COLOR 10060364 

//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, color, subtype
  float modifier


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  event = self.event & 7
  if((self.event & 8))
    ButtonHandled()
    return 0
  else
    if(STANDARD.RiderDismountButton())
      return 0
    if(event == BUTTON_LEFT || event == BUTTON_RIGHT || event == BUTTON_SPECIAL1 || event == BUTTON_SPECIAL2)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        if(self.flags & CHAR_ON_GROUND)
          self.setimer = 30  // Time that bump damage is active
          self.sflags = self.sflags | SCRIPT_BUMP_DAMAGE
          self.action = ACTION_BASH_LEFT
          self.daction = ACTION_STAND
          modifier = 0.7 + (self.dex * 0.02)
          self.vx = (self.frontx * modifier)
          self.vy = (self.fronty * modifier)
          self.vz = 0.4
          self.z = self.z + 0.5
          ButtonHandled()
          color = WHITE
          subtype = self.eqcol01>>6
          if(subtype == 0)
            // Tranch
            color = 8677689
          if(subtype == 1)
            // Blue Recluse
            color = 3760516
          if(subtype == 2)
            // Blood Widow
            color = 536642
          if(subtype == 3)
            // Spider Queen
            color = 5382267
          PBLUR.Effect(color, 3.0, 1.0, 255, 2.0)
          PlaySound("FILE:-LURCH.RAW", 256, 128)
          return 0


//-----------------------------------------------------------------------
int Event()
  int self, index
  int target, oldtarget
  float oldvx, oldvy


  STANDARD.DamageSoundEvent("FILE:-BUGHIT.RAW", NULL, NULL)


  // Make mount/dismount work nicer...
  self = FindSelf()
  if(self.event == EVENT_MOUNTED)
    self.aitimer = 0
    return 0
  if(self.event == EVENT_DISMOUNTED)
    index = FindIndex(self)
    if(IndexIsLocalPlayer(index) == FALSE)
      // Tranch isn't a player, so turn it's AI back on...
      self.aitimer = 20
    return 0
  if(self.event == EVENT_SECONDARY_TIMER)
    self.sflags = self.sflags & (255-SCRIPT_BUMP_DAMAGE)
  if(self.event == EVENT_HIT_CHARACTER && self.action == ACTION_BASH_LEFT)
    oldtarget = self.target
    self.target = SystemGet(SYS_COLLISIONCHAR, 0, 0)
    target = FindTarget()
    if(target)
      oldvx = self.vx
      oldvy = self.vy
      if(STANDARD.BumpDamageEvent(DAMAGE_EDGE, self.str>>3, 1, TRUE, TRUE))
        self.spin = self.spin+32768
        self.vx = oldvx
        self.vy = oldvy
        if(target.nctimer < 40)
          target.nctimer = 40
        self.rltimer = 5
        STANDARD.PoisonCharacter(self.target, FindIndex(self), (self.str<<5) + 200)
        SystemSet(SYS_BUMPABORT, 0, 0, 0)
    self.target = oldtarget
    return 0
  STANDARD.Event()
  STANDARD.LevelUpEvent(4, 0, 2, 4, 0)



//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6

  ModelAssign(self+MODEL_BASE_FILE, "FILE:TRANCH.RDY")
  if(subtype == 0)
    // Tranch
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DYELLOW.RGB")
    ModelAssign(self+MODEL_BASE_TEX2, "FILE:=TRANCH0.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, WHITE)
  if(subtype == 1)
    // Blue Recluse
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DBLUE.RGB")
    ModelAssign(self+MODEL_BASE_TEX2, "FILE:=TRANCH2.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, YELLOW)
  if(subtype == 2)
    // Blood Widow
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:GBLACK.RGB")
    ModelAssign(self+MODEL_BASE_TEX2, "FILE:=TRANCH1.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, DARK_RED)
  if(subtype == 3)
    // Spider Queen
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:NPURPLE.RGB")
    ModelAssign(self+MODEL_BASE_TEX2, "FILE:=TRANCH2.RGB")
    ModelAssign(self+MODEL_BASE_COLOR, GREEN)
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:MUSHI.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01 >> 6
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Tranch", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Blue Recluse", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Blood Widow", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Spider Queen", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  int self, target, subtype
  float x, y, distance
  self = FindSelf()
  subtype = self.eqcol01>>6


  STANDARD.AIUseTripe()
  self.target = AcquireTarget(ACQUIRE_SEE_BEHIND | ACQUIRE_SEE_ENEMY, NULL, 75.0)
  target = FindTarget()
  if(target)
    self.gotox = target.x
    self.gotoy = target.y
    FindPath()
    self.gotox = self.gotox + (RollDice(1, 5)-3)
    self.gotoy = self.gotoy + (RollDice(1, 5)-3)

    x = self.x - target.x
    y = self.y - target.y
    distance = x*x + y*y
    x = self.boxsize + target.boxsize + ((subtype+1)*6.0)
    x = x*x
    if(distance < x)
      // Start goin' directly at target...
      self.gotox = target.x
      self.gotoy = target.y
      if(distance < (x*0.25))
        // Close enough to strike...
        if(GetDirection(target.x, target.y) == DIRECTION_FRONT && target.daction != ACTION_KNOCK_OUT)
          // We're facing the enemy...
          ButtonPress(BUTTON_PRESS, BUTTON_RIGHT, 0)
        else
          // Not facing correct direction...  Make sure we're not too close...
          if(distance < 4.0)
            self.gotox = self.gotox + (self.frontx*-4.0)
            self.gotoy = self.gotoy + (self.fronty*-4.0)
    self.aitimer = 30 + RollDice(1, 10) - (subtype*5)
  else
    self.gotox = self.x
    self.gotoy = self.y
  self.aitimer = 10 + RollDice(1, 10)


//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = self.eqcol01 >> 6

  self.boxsize = 1.8
  self.height = 3
  self.flags = CHAR_GRAVITY_ON | CHAR_CAN_BE_MOUNTED
  self.sflags = SCRIPT_NO_KNOCK_OUT
  self.mflags = MORE_FLAG_FAST_TURN
  self.vflags = VIRTUE_FLAG_RUN_ME_OVER | VIRTUE_FLAG_EATS_TRIPE
  self.oflags = OTHER_FLAG_EASY_ENTANGLE
  self.timer = 1

  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  STANDARD.TreasureSetup(difficulty, 10, 0.0, ITEM_NONE, ITEM_NONE)
  self.hitsmax = 4*difficulty
  self.hits = self.hitsmax
  self.dex = 5*difficulty
  self.str = 7*difficulty
  self.vspin = 160 + (60*difficulty)
  self.defbash = -2
  self.defacid = subtype+1
  self.deffire = -1
  ModelSetup()
  STANDARD.PutOnMount()

//-----------------------------------------------------------------------
