#define BIG_SCALE 1.5

//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.ButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self, subtype

  self = FindSelf()
  subtype = (self.eqcol01>>6)


  STANDARD.DamageSoundEvent("FILE:-WAR01.RAW", "FILE:-WAR02.RAW", NULL)


  // Boss Defeat stuff...
  if(subtype == 3)
    if(self.hits == 0 && self.event == EVENT_DAMAGED)
      self.tetimer = 20
      if(self.action == ACTION_KNOCK_OUT || self.action == ACTION_KNOCK_OUT_STUN)
        self.tetimer = 0
        STANDARD.BossDefeatStart()
        self.hoverz = self.z + 5.0
        self.flags = self.flags & (65535-CHAR_GRAVITY_ON)
        self.flags = self.flags | CHAR_HOVER_ON
        self.daction = ACTION_SPECIAL_9
        self.daction = ACTION_SPECIAL_9
        return 0
    if(self.hits == 0 && (self.event == EVENT_THIRDIARY_TIMER || self.event == EVENT_HIT_FLOOR))
      STANDARD.BossDefeatStart()
      self.hoverz = self.z + 2.0
      self.flags = self.flags & (65535-CHAR_GRAVITY_ON)
      self.flags = self.flags | CHAR_HOVER_ON
      self.action = ACTION_SPECIAL_9
      self.daction = ACTION_SPECIAL_9
      return 0


  STANDARD.Event()
  STANDARD.LevelUpEvent(4, 4, 0, 0, 2)



//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype


  // Funky shield stuff...
  FUNKY.ShieldUnpress()
  ModelClear()


  self = FindSelf()
  subtype = (self.eqcol01>>6)
  ModelAssign(self+MODEL_BASE_FILE, "FILE:PLAYER.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

  if(subtype == 0)
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:NEWWAR.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:FLESH.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:GREEN.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:DORANGE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:SHADWAR0.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)
  if(subtype == 1)
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:NEWWAR.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:FLESH.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:BLACK.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:SHADWAR0.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)
  if(subtype == 2)
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:NEWWAR.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:FLESH.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:RED.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:DBLUE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:SHADWAR0.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)
  if(subtype == 3)
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:NEWWAR.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:FLESH.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:BLACK.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:DMAGENTA.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:SHADWAR0.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, WHITE)

    ModelAssign(self+MODEL_PADS_FILE, "FILE:WARPADS.RDY")
    ModelAssign(self+MODEL_PADS_TEX0, "FILE:DMAGENTA.RGB")
    ModelAssign(self+MODEL_PADS_TEX1, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_PADS_TEX2, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_PADS_TEX3, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_PADS_COLOR, WHITE)

  ModelAssign(self+MODEL_EYES_FILE, "FILE:WAREYES0.RDY")
  ModelAssign(self+MODEL_EYES_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_EYES_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_EYES_TEX2, "FILE:WHITE.RGB")
  STANDARD.EyeSetup(self+MODEL_EYES_TEX3, self.team)
  ModelAssign(self+MODEL_EYES_COLOR, WHITE)

  STANDARD.ItemModelSetup(MODEL_LEFT_FILE)
  STANDARD.ItemModelSetup(MODEL_RIGHT_FILE)

//-----------------------------------------------------------------------
int GetName()
  int self, subtype

  self = FindSelf()
  subtype = (self.eqcol01>>6)
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Enchanter", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Summoner", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Warlock", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Noggindorf", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUsePotions()
  CCARRION.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  int self
  STANDARD.BossDefeatRefresh()
  self = FindSelf()
  self.frontx = self.frontx*BIG_SCALE
  self.fronty = self.fronty*BIG_SCALE
  self.frontz = self.frontz*BIG_SCALE
  self.sidex = self.sidex*BIG_SCALE
  self.sidey = self.sidey*BIG_SCALE
  self.sidez = self.sidez*BIG_SCALE
  self.upx = self.upx*BIG_SCALE
  self.upy = self.upy*BIG_SCALE
  self.upz = self.upz*BIG_SCALE
  self.timer = 1

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>6)


  self.boxsize = 1.25
  self.height = 6
  self.flags = CHAR_GRAVITY_ON
  self.vspin = 700
  self.timer = 0
  self.eqspec1 = ITEM_JUMP


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.5)
  STANDARD.TreasureSetup(difficulty, 10, 1.1, ITEM_MANA, ITEM_SUPER_MANA)
  self.hitsmax = (6*difficulty)+5
  self.manamax = (5*difficulty)+12
  self.mana = self.manamax
  self.str = (3*difficulty)+8
  self.dex = (2*difficulty)+4
  self.int = (15*difficulty)+15


  self.defedge = subtype+1
  self.defbash = (subtype+1)>>1
  self.defacid = 1 + subtype
  self.defvolt = 1 + subtype
  self.deffire = 1 + subtype
  self.defice  = 1 + subtype


  if(subtype == 0)
    self.eqleft = ITEM_SPELL_ENCHANT
    self.eqspec2 = ITEM_SPELL_ENTANGLE
    if(RollDice(1, 10) > 5)
      self.eqspec2 = ITEM_SPELL_KNOCK
    self.eqright = ITEM_DAGGER
  if(subtype == 1)
    self.eqleft = ITEM_SPELL_PORTAL
    self.eqspec2 = ITEM_SPELL_LIGHTNING_BOLT
    if(RollDice(1, 10) > 5)
      self.eqspec2 = ITEM_SPELL_POISON_CLOUD
    self.eqright = ITEM_DAGGER
  if(subtype == 2)
    self.eqleft = ITEM_SPELL_TELEPORT
    self.eqspec2 = ITEM_SPELL_FIREBALL
    if(RollDice(1, 10) > 5)
      self.eqspec2 = ITEM_SPELL_MORPH
    self.eqright = ITEM_DAGGER
    self.eflags = ENCHANT_FLAG_DEFLECT
  if(subtype == 3)
    self.eqright = ITEM_SWORD | WEAPON_VARIANT_UNHOLY
    self.eqleft = ITEM_SPELL_TELEPORT
    self.eqspec2 = ITEM_SPELL_LIGHTNING_BOLT
    self.boxsize = self.boxsize*BIG_SCALE
    self.height = self.height*BIG_SCALE
    self.timer = 1
    self.hitsmax = self.hitsmax * 4
    self.eflags = ENCHANT_FLAG_DEFLECT
  self.hits = self.hitsmax


  STANDARD.FixStats(self)
  ModelSetup()


//-----------------------------------------------------------------------
