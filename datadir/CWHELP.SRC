//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype
  self = FindSelf()

  subtype = ((self.eqcol01>>4) * 5)>>4
  ModelAssign(self+MODEL_BASE_FILE, "FILE:WHELP.RDY")
  if(subtype == 0)
    // Red Whelp
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:RED.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:YELLOW.RGB")
  if(subtype == 1)
    // Green Whelp
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:YELLOW.RGB")
  if(subtype == 2)
    // Blue Whelp
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:DBLUE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:RED.RGB")
  if(subtype == 3)
    // Black Whelp
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:BLACK.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:PURPLE.RGB")
  if(subtype == 4)
    // White Whelp
    ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_BASE_TEX1, "FILE:BLUE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:SHADWELP.RGB")
  STANDARD.EyeSetup(self+MODEL_BASE_TEX3, self.team)
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()

  subtype = ((self.eqcol01>>4) * 5)>>4
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Red Dragon Whelp", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Green Dragon Whelp", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Blue Dragon Whelp", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Black Dragon Whelp", 256)
  if(subtype == 4)
    StringAppend(NAME_STRING, "White Dragon Whelp", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, cost, i, subtype
  float x, y, z, vx, vy, vz


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  self = FindSelf()
  event = self.event & 7
  if((self.event & 8))
    ButtonHandled()
    if(event == BUTTON_SPECIAL1 || event == BUTTON_SPECIAL2)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND || self.action == ACTION_BASH_RIGHT)
        if(self.mana > 0)
          if(self.charge < 64)
            self.charge = 64
          cost = (self.charge>>6)
          if(cost > self.mana)
            cost = self.mana
            self.charge = cost<<6
          self.mana = self.mana - cost


          self.action = ACTION_BASH_LEFT
          self.daction = ACTION_STAND
          self.sflags = self.sflags & (255-SCRIPT_BUMP_DAMAGE)
          x = self.x + self.frontx*5.0
          y = self.y + self.fronty*5.0
          z = self.z + 3.5
          subtype = ((self.eqcol01>>4) * 5)>>4
          if(subtype == 2)
            AutoAim(5.0, 0.05, self.spin, self.team, self.dex, 8192, AUTOAIM_CRUNCH)
          else
            AutoAim(0.5, 0.05, self.spin, self.team, self.dex, 8192, AUTOAIM_CRUNCH)
          vx = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_X)
          vy = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Y)
          vz = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Z)

          CDRAGON.ShotEffect(x, y, z, vx, vy, vz, self.charge>>1)
      self.charge = 0
      return 0
    if(event == BUTTON_LEFT || event == BUTTON_RIGHT)
      if(self.action == ACTION_BASH_RIGHT)
        self.daction = ACTION_STAND
        self.sflags = self.sflags & (255-SCRIPT_BUMP_DAMAGE)
      return 0
  else
    if(STANDARD.RiderDismountButton())
      return 0
    if(self.event == BUTTON_LEFT || self.event == BUTTON_RIGHT)
      if(self.action == ACTION_WALK)
        self.action = ACTION_BASH_RIGHT
        self.daction = ACTION_BASH_RIGHT
        self.sflags = SCRIPT_BUMP_DAMAGE
        ButtonHandled()
        return 0
    if(event == BUTTON_SPECIAL1 || event == BUTTON_SPECIAL2)
      if(self.action == ACTION_STAND || self.action == ACTION_WALK || self.action == ACTION_BASH_RIGHT)
        // Do breath weapon charging...
        x = self.x + self.frontx*3.5
        y = self.y + self.fronty*3.5
        z = self.z + 5.5

        // Increment the charge
        i = 0
        while(i < 4)
          if(self.charge < 255 && self.charge < (self.mana<<6))
            self.charge++
          i++


        CDRAGON.ChargeEffect(x, y, z, self.charge>>1)
        return 0

//-----------------------------------------------------------------------
int Event()
  int self, index, target, child, rider
  float oldvx, oldvy
  float angle, x, y


  STANDARD.DamageSoundEvent("FILE:-LIZZ01.RAW", "FILE:-LIZZ02.RAW", "FILE:-LIZZ03.RAW")


  // Do trample stuff...
  self = FindSelf()
  if(self.action == ACTION_BASH_RIGHT)
    oldvx = self.vx
    oldvy = self.vy
    if(STANDARD.BumpDamageEvent(DAMAGE_EDGE, self.str>>3, 1, TRUE, FALSE))
      self.spin = self.spin+32768
      self.vx = oldvx
      self.vy = oldvy
      self.rltimer = 5
      target = FindByIndex(SystemGet(SYS_COLLISIONCHAR, 0, 0))
      if(target)
        if(target.nctimer < 40)
          target.nctimer = 40
      SystemSet(SYS_BUMPABORT, 0, 0, 0)
      return 0


  // Make mount/dismount work nicer...
  if(self.event == EVENT_MOUNTED)
    self.aitimer = 0
    return 0
  if(self.event == EVENT_DISMOUNTED)
    index = FindIndex(self)
    if(IndexIsLocalPlayer(index) == FALSE)
      // Whelp isn't a player, so turn it's AI back on...
      self.aitimer = 20
    return 0


  // Mana recharge...
  if(self.event == EVENT_THIRDIARY_TIMER)
    if(self.mana < (self.manamax-self.manatap))
      self.mana++
    CDRAGON.ResetManaTimer()


  // Normal stuff...
  STANDARD.Event()
  if(STANDARD.LevelUpEvent(4, 2, 0, 2, 2) == TRUE)
    // Change form...
    if(STANDARD.LevelUpChange("FILE:CDRAGON.RUN"))
      return TRUE


  // Don't knock back as far...
  if(self.action == ACTION_KNOCK_OUT_BEGIN)
    if(self.vz > 0.59)
      self.vz = self.vz * 0.75

  // Knock out if charging into a wall...
  if(self.event == EVENT_HIT_WALL)
    if(self.action == ACTION_BASH_RIGHT)
      self.spin = self.spin+32768
      angle = (self.spin*360)/65536.0
      x = sin(angle+90.0)*0.5
      y = sin(angle)*0.5
      Dismount()
      self.combo = 0
      self.action = ACTION_KNOCK_OUT_BEGIN
      self.daction = ACTION_KNOCK_OUT
      self.evtimer = 80+RollDice(1, 40)
      if(self.eaction == 0)
        // Blink eyes
        self.eaction = 1
      self.theight = self.height
      self.height = 3
      self.vx = x
      self.vy = y
      self.vz = 0.60
      self.flags = self.flags | CHAR_FALL_ON
      self.nctimer = 30


  if(self.action == ACTION_KNOCK_OUT_BEGIN)
    self.charge = 0
    ButtonPress(BUTTON_UNPRESS, BUTTON_SPECIAL2, 0)
  if(self.action != ACTION_BASH_RIGHT)
    self.sflags = self.sflags & (255-SCRIPT_BUMP_DAMAGE)


//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUsePotions()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()


//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self
  self = FindSelf()

  self.boxsize = 2.5
  self.height = 4
  self.flags = CHAR_GRAVITY_ON | CHAR_CAN_BE_MOUNTED | CHAR_NO_BACKSTAB
  self.vflags = VIRTUE_FLAG_IMMUNE | VIRTUE_FLAG_EATS_TRIPE
  self.vspin = 1024
  self.timer = 1
  self.charge = 0

  CDRAGON.SetupStats()
  CDRAGON.ResetManaTimer()
  self.hitsmax = self.hitsmax>>1
  self.hits = self.hits>>1
  self.manamax = self.manamax>>1
  self.mana = self.mana>>1
  self.str = self.str>>1
  self.dex = self.dex>>1
  self.int = self.int>>1
  if(self.defedge < 20)
    self.defedge = self.defedge>>1
  if(self.defbash < 20)
    self.defbash = self.defbash>>1
  if(self.defice < 20)
    self.defice = self.defice>>1
  if(self.deffire < 20)
    self.deffire = self.deffire>>1
  if(self.defacid < 20)
    self.defacid = self.defacid>>1
  if(self.defvolt < 20)
    self.defvolt = self.defvolt>>1
  STANDARD.TreasureSetup(1.0, self.hitsmax*3, 0.0, ITEM_NONE, ITEM_MEAT)


  ModelSetup()
  STANDARD.PutOnMount()


//-----------------------------------------------------------------------

