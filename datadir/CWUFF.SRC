#define FIRE_SMOKE_COLOR (128)
#define ICE_SMOKE_COLOR (4239600)

//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, color, subtype
  float modifier


  self = FindSelf()
  event = self.event & 7
  if((self.event & 8))
    if(event == BUTTON_SPECIAL1 || event == BUTTON_SPECIAL2)
      if(self.daction == ACTION_THRUST_LEFT)
        self.action = ACTION_THRUST_RIGHT
        self.daction = ACTION_STAND
        self.charge = 0
        self.vspin = 768
      else
        if(self.action == ACTION_EXTRA)
          return 0
        self.vspin = 768
      ButtonHandled()


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  if((self.event & 8))
    if(event == BUTTON_LEFT || event == BUTTON_RIGHT)
      if(self.daction == ACTION_BLOCK)
        self.vx = self.gotox - self.x
        self.vy = self.gotoy - self.y
        modifier = self.vx*self.vx + self.vy*self.vy
        if(modifier > 1.0)
          modifier = sqrt(modifier)
          self.vx = self.vx / modifier
          self.vy = self.vy / modifier
          modifier = 0.7 + (self.dex * 0.01) + (self.charge * 0.005)
          self.vx = (self.vx * modifier)
          self.vy = (self.vy * modifier)
          self.vspin = 3200
          self.setimer = 10  // Time before resetting vspin (also used to turn off bump damage)
          self.sflags = self.sflags | SCRIPT_BUMP_DAMAGE
          self.action = ACTION_BASH_LEFT
          self.daction = ACTION_STAND
          self.vz = 0.4
          color = WHITE
          subtype = self.eqcol01>>6
          if(subtype == 0)
            // Wuff
            color = 8737850
          if(subtype == 1)
            // Dire Wuff
            color = 6503045
          if(subtype == 2)
            // Winter Wuff
            color = 4095401
          if(subtype == 3)
            // Hot Dog
            color = 8731706
          PBLUR.Effect(color, 4.0, 0.5, 255, 2.0)
          PlaySound("FILE:-GROWL.RAW", 255, 128)
        else
          self.action = ACTION_BLOCK_END
          self.daction = ACTION_STAND
          self.vx = 0.0
          self.vy = 0.0
      ButtonHandled()
      return 0
  else
    if(STANDARD.RiderDismountButton())
      return 0
    if(event == BUTTON_LEFT || event == BUTTON_RIGHT)
      // Charge for leap
      if(self.flags & CHAR_ON_GROUND)
        if(self.action == ACTION_WALK || self.action == ACTION_STAND)
          self.action = ACTION_BLOCK_BEGIN
          self.daction = ACTION_BLOCK
          self.charge = 0
        else
          if(self.action == ACTION_BLOCK)
            if(self.charge < 250)
              self.charge = self.charge+4
            else
              ButtonHandled()
      return 0
    if(event == BUTTON_SPECIAL1 || event == BUTTON_SPECIAL2)
      if(self.flags & CHAR_ON_GROUND)
        if(self.action == ACTION_WALK || self.action == ACTION_STAND)
          subtype = self.eqcol01>>6
          if(subtype < 2)
            // Howl
            self.action = ACTION_EXTRA
            self.daction = ACTION_STAND
            self.vspin = 0
            PlaySound("FILE:-WUFF00.RAW", 256, 255)
          else
            // Breath attack
            self.action = ACTION_BASH_RIGHT
            self.daction = ACTION_THRUST_LEFT
            self.vspin = 0
          ButtonHandled()
      return 0

//-----------------------------------------------------------------------
int Event()
  int self, subtype, child, index
  int target, oldtarget
  float oldvx, oldvy


  // Make mount/dismount work nicer...
  self = FindSelf()
  STANDARD.DamageSoundEvent("FILE:-WUFF01.RAW", "FILE:-WUFF02.RAW", "FILE:-WUFF03.RAW")
  if(self.event == EVENT_MOUNTED)
    self.aitimer = 0
    return 0
  if(self.event == EVENT_DISMOUNTED)
    index = FindIndex(self)
    ButtonPress(BUTTON_UNPRESS, BUTTON_SPECIAL2, 0)
    if(IndexIsLocalPlayer(index) == FALSE)
      // Wuff isn't a player, so turn it's AI back on...
      self.aitimer = 20
    return 0
  if(self.event == EVENT_SECONDARY_TIMER)
    if(self.vspin > 768)
      self.vspin = 768
      if(self.sflags & SCRIPT_BUMP_DAMAGE)
        self.setimer = 10
    else
      self.sflags = self.sflags & (255-SCRIPT_BUMP_DAMAGE)
  if(self.event == EVENT_HIT_CHARACTER && self.action == ACTION_BASH_LEFT)
    oldtarget = self.target
    self.target = SystemGet(SYS_COLLISIONCHAR, 0, 0)
    target = FindTarget()
    if(target)
      oldvx = self.vx
      oldvy = self.vy
      if(STANDARD.BumpDamageEvent(DAMAGE_EDGE, self.str>>3, 1, TRUE, TRUE))
        self.spin = self.spin+32768
        self.vx = oldvx
        self.vy = oldvy
        if(target.nctimer < 40)
          target.nctimer = 40
        self.rltimer = 5
        SystemSet(SYS_BUMPABORT, 0, 0, 0)
    self.target = oldtarget
    return 0
  STANDARD.Event()
  STANDARD.LevelUpEvent(6, 0, 0, 4, 0)
  if(self.event == EVENT_DAMAGED)
    self.vspin = 768
    if(self.daction == ACTION_KNOCK_OUT && self.action == ACTION_KNOCK_OUT_STUN)
      // Make two headed wuff lose one of its heads...
      if((self.eqcol01 & 8) && self.team != TEAM_GOOD)
        GoPoof(POOF_STUCK_PARTICLES)
        self.eqcol01 = self.eqcol01 & 247
        child = PZOMHEAD.Effect(self.x, self.y, self.z+3.5, "FILE:=WUFHEAD.RGB")
        if(child)
          child.vx = child.vx + self.vx*0.40
          child.vy = child.vy + self.vy*0.40
        ModelSetup()

//-----------------------------------------------------------------------
int FrameEvent()
  int self, subtype, two_heads, child
  int image, type, damage, wound, smoke_color, special_flags
  float x, y, z, frontx, fronty

  self = FindSelf()
  subtype = (self.eqcol01>>6)
  if(self.event & 8)
    if(self.action == ACTION_EXTRA && subtype == 1)
      // Summon a helper Wuff...
      if(ChildCount(self) < MaxHelpers(self, 2))
        SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TEAM, self.team)
        SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_SUBTYPE, 0)
        SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_OWNER, FindIndex(self))
        if(FindByIndex(self.owner))
          SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_OWNER, self.owner)
        child = Spawn(CHARACTER, self.x, self.y, self.z, 0)
        if(child)
          child.frame = self.frame+1
          child.action = self.action
          child.daction = self.daction
          child.aitimer = 30
          child.spin = self.spin
          SetupTargetMatrix(child)
    if(self.action == ACTION_THRUST_LEFT)
      // Spawn a flame thrower/snow blower particle for the Wuff...
      if(subtype == 2 || subtype == 3)
        if(self.charge < 220)
          if(self.charge < 200)
            self.charge = self.charge+35
          self.charge++
        else
          self.charge = 200

        special_flags = SPECIAL_FLAG_CHOP_THROUGH
        if(subtype == 2)
          // Cone of Cold
          smoke_color = ICE_SMOKE_COLOR
          image = "FILE:ICE1.RGB"
          type = DAMAGE_ICE
          damage = RollDice(1, 3)
          wound = 0
        else
          // Cone of Fire
          smoke_color = FIRE_SMOKE_COLOR
          image = "FILE:FIRE1.RGB"
          type = DAMAGE_FIRE
          damage = RollDice(1, 4)
          wound = 0


        two_heads = (self.eqcol01 & 8)
        if(two_heads)
          frontx = self.frontx*0.939 + self.sidex*0.342
          fronty = self.fronty*0.939 + self.sidey*0.342
          x = self.x + frontx*2.0
          y = self.y + fronty*2.0
          z = self.z + 1.5
          PCONE.Effect(x, y, z, frontx, fronty, 0.05, type, damage, wound, special_flags, self.charge, smoke_color, image)

          frontx = self.frontx*0.939 - self.sidex*0.342
          fronty = self.fronty*0.939 - self.sidey*0.342
          x = self.x + frontx*2.0
          y = self.y + fronty*2.0
          PCONE.Effect(x, y, z, frontx, fronty, 0.05, type, damage, wound, special_flags, self.charge, smoke_color, image)
        else
          x = self.x + self.frontx*2.0
          y = self.y + self.fronty*2.0
          z = self.z + 1.5
          PCONE.Effect(x, y, z, self.frontx, self.fronty, 0.05, type, damage, wound, special_flags, self.charge, smoke_color, image)



//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype, skin_color, hair_color, eye_color, teeth_color, two_heads
  self = FindSelf()
  subtype = (self.eqcol01>>6)
  two_heads = (self.eqcol01 & 8)

  ModelAssign(self+MODEL_BASE_FILE, "FILE:WUFFANIM.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

  if(subtype == 0)
    // Wuff
    skin_color = "FILE:DORANGE.RGB"
    hair_color = "FILE:LORANGE.RGB"
    eye_color = WHITE
    teeth_color = WHITE
  if(subtype == 1)
    // Dire Wuff
    skin_color = "FILE:DPURPLE.RGB"
    hair_color = "FILE:LPURPLE.RGB"
    eye_color = YELLOW
    teeth_color = WHITE
  if(subtype == 2)
    // Winter Wuff
    skin_color = "FILE:BLUE.RGB"
    hair_color = "FILE:WHITE.RGB"
    eye_color = WHITE
    teeth_color = WHITE
  if(subtype == 3)
    // Hot Dog
    skin_color = "FILE:DRED.RGB"
    hair_color = "FILE:RED.RGB"
    eye_color = YELLOW
    teeth_color = YELLOW


  if(two_heads)
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:WUFFBOD2.RDY")
  else
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:WUFFBODY.RDY")
  ModelAssign(self+MODEL_CHEST_TEX0, skin_color)
  ModelAssign(self+MODEL_CHEST_TEX1, hair_color)
  ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=WUFF.RGB")
  ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=WUFF.RGB")
  ModelAssign(self+MODEL_CHEST_COLOR, teeth_color)


  if(two_heads)
    ModelAssign(self+MODEL_HEAD_FILE, "FILE:WUFFNOS2.RDY")
  else
    ModelAssign(self+MODEL_HEAD_FILE, "FILE:WUFFNOSE.RDY")
  ModelAssign(self+MODEL_HEAD_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_HEAD_TEX1, "FILE:GBLACK.RGB")
  ModelAssign(self+MODEL_HEAD_TEX2, "FILE:WHITE.RGB")
  STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
  ModelAssign(self+MODEL_HEAD_COLOR, eye_color)


//-----------------------------------------------------------------------
int GetName()
  int self, subtype

  self = FindSelf()
  subtype = (self.eqcol01>>6)
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Wuff", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Dire Wuff", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Winter Wuff", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Hot Dog", 256)
  STANDARD.NameAppendHits()


//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUseTripe()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>6)


  // Hot Dogs start with 2 heads...
  self.eqcol01 = (self.eqcol01 & 247)
  if(subtype == 3)
    self.eqcol01 = self.eqcol01 | 8


  self.boxsize = 2.4
  self.height = 3
  self.flags = CHAR_GRAVITY_ON | CHAR_CAN_BE_MOUNTED
  self.mflags = MORE_FLAG_FAST_TURN
  self.vflags = VIRTUE_FLAG_EATS_TRIPE
  self.timer = 1




  self.hits = 10
  self.hitsmax = 10
  self.dex = 20


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.7)
  STANDARD.TreasureSetup(difficulty, 8, 0.0, ITEM_NONE, ITEM_MEAT)
  self.hitsmax = (5*difficulty)+8
  self.hits = self.hitsmax
  self.str = (4*difficulty)+8
  self.dex = (5*difficulty)+6
  self.int = (2+difficulty)+5
  self.defedge = subtype
  self.defbash = subtype>>1
  self.defacid = 1
  self.defice = 2
  self.deffire = -1
  if(subtype == 2)
    self.deffire = -4
    self.defice  = 8
  if(subtype == 3)
    self.deffire = 8
    self.defice = -4
  self.vspin = 768+(subtype<<8)



  ModelSetup()
  STANDARD.PutOnMount()


//-----------------------------------------------------------------------
