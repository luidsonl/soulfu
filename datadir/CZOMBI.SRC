// !!!BAD!!!
// !!!BAD!!!  Need hole particle for Zombi...  Also warning marker thing...
// !!!BAD!!!
#define FIRE_SMOKE_COLOR (128)
#define ICE_SMOKE_COLOR (4239600)
#define POISON_SMOKE_COLOR (192<<8)

//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, subtype
  self = FindSelf()


  if(STANDARD.RestrictedButtonEvent())
    return TRUE


  if(self.daction == ACTION_SPECIAL_0)
    self.action = ACTION_SPECIAL_1
    self.daction = ACTION_STAND
    self.alpha = 255
    self.flags = self.flags & (65535-CHAR_NO_COLLISION)
    ButtonHandled()
    PlaySound("FILE:-STATU03.RAW", 256, 64)
    CSTATUE.RubbleEffect(self.x, self.y, self.z, self.boxsize+1.0, 2, FALSE)
    return 0
  event = self.event & 7
  if(!(self.event & 8))
    if(event == BUTTON_LEFT)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        self.action = ACTION_BASH_LEFT
        self.daction = ACTION_STAND
        ButtonHandled()
      return 0
    if(event == BUTTON_RIGHT && self.eqright == ITEM_NONE)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        self.action = ACTION_BASH_RIGHT
        self.daction = ACTION_STAND
        ButtonHandled()
      return 0
    if(event == BUTTON_SPECIAL2 && self.eqspec2 == ITEM_NONE)
      if(self.action == ACTION_WALK || self.action == ACTION_STAND)
        subtype = self.eqcol01>>6
        if(subtype > 0)
          if(self.eqcol01 & 8)
            self.action = ACTION_SPECIAL_2
            self.daction = ACTION_STAND
            self.charge = 20
        ButtonHandled()
      return 0

  STANDARD.ButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self, subtype, child


  self = FindSelf()
  subtype = self.eqcol01>>6
  if(self.event == EVENT_SECONDARY_TIMER)
    // Restore hits...
    RestoreCharacterHits(self, 1, 1)
    self.setimer = 300+(RollDice(1, 200)*4)

    // Zombi sounds...
    if(subtype < 2)
      // Only talk if they've got heads...
      if(self.eqcol01 & 8)
        // And only if not hiding or KO'd...
        if(self.daction != ACTION_SPECIAL_0 && self.daction != ACTION_KNOCK_OUT)
          if(RollDice(1, 100) < 5 && self.team == TEAM_GOOD)
            // I love brains to eat...
            PlaySound("FILE:ZOMBI1.RAW", 210+RollDice(1, 20), 255)
          else
            // Brains...
            PlaySound("FILE:ZOMBI0.RAW", 230+RollDice(1, 20), 255)
  if(self.event == EVENT_RIPPLE || self.event == EVENT_HIT_WATER)
    if(self.daction == ACTION_SPECIAL_0)
      // Hidden zombi's shouldn't make ripples...
      return 0
  STANDARD.Event()
  if(self.event == EVENT_DAMAGED)
    if(self.daction == ACTION_KNOCK_OUT && self.action == ACTION_KNOCK_OUT_STUN)
      // Make zombis lose their heads...
      if(subtype < 2 && self.team != TEAM_GOOD)
        // But only if they still got one...
        if(self.eqcol01 & 8)
          // And only every so often...
          GoPoof(POOF_STUCK_PARTICLES)
          self.eqcol01 = self.eqcol01 & 247
          if(subtype == 0)
            child = PZOMHEAD.Effect(self.x, self.y, self.z+3.5, "FILE:=ZOMHEAD.RGB")
          else
            child = PZOMHEAD.Effect(self.x, self.y, self.z+3.5, "FILE:=GOLHEAD.RGB")
          if(child)
            child.vx = child.vx + self.vx*0.40
            child.vy = child.vy + self.vy*0.40
          ModelSetup()

  if(self.event == EVENT_LEVEL_UP && self.team == TEAM_GOOD)
    STANDARD.LevelUpEvent(6, 0, 4, 0, 0)
    if(IndexIsLocalPlayer(FindIndex(self)) || IndexIsLocalPlayer(self.owner))
      // Change form...
      if(subtype == 0 && self.level == 3)
        // We're a zombi becoming a ghoul...
        self.eqcol01 = self.eqcol01+64
        self.defacid = self.defacid+2
        self.deffire = self.deffire-2
        ModelSetup()
        return TRUE
      if(subtype == 1 && self.level == 5)
        // We're a ghoul becoming a mummy...
        self.eqcol01 = self.eqcol01+64
        self.defacid = self.defacid+2
        self.deffire = self.deffire-2
        self.eflags = self.eflags & (255-ENCHANT_FLAG_SUMMON_1)
        ModelSetup()
        return TRUE

//-----------------------------------------------------------------------
int FrameEvent()
  int self, subtype, special_flags, damage, wound
  int image, smoke_color, type, dex
  float x, y, z, vx, vy, vz, offset
  self = FindSelf()


  if(self.event & 4)
    subtype = self.eqcol01>>6
    if(subtype > 0)
      if(self.charge < 200)
        self.charge = self.charge+10
      else
        self.charge = 208
      offset = (self.charge*0.005)+1.5


      special_flags = SPECIAL_FLAG_CHOP_THROUGH
      if(subtype == 1)
        // Cone of Poison
        smoke_color = POISON_SMOKE_COLOR
        image = "FILE:POISON1.RGB"
        type = DAMAGE_ACID
        damage = RollDice(1, 2)
        wound = 0
        special_flags = special_flags | SPECIAL_FLAG_POISON
      if(subtype == 2)
        // Cone of Cold
        smoke_color = ICE_SMOKE_COLOR
        image = "FILE:ICE1.RGB"
        type = DAMAGE_ICE
        damage = RollDice(1, 3)
        wound = 0
      if(subtype == 3)
        // Cone of Fire
        smoke_color = FIRE_SMOKE_COLOR
        image = "FILE:FIRE1.RGB"
        type = DAMAGE_FIRE
        damage = RollDice(1, 4)
        wound = 0


      x = self.x + self.frontx*offset
      y = self.y + self.fronty*offset
      z = self.z + 2.8
      PCONE.Effect(x, y, z, self.frontx, self.fronty, 0.05, type, damage, wound, special_flags, self.charge, smoke_color, image)
  if(self.event & 8)
    subtype = self.eqcol01>>6
    special_flags = 0
    type = DAMAGE_BASH
    if(subtype == 1)
      special_flags = SPECIAL_FLAG_POISON
    if(subtype == 2)
      special_flags = SPECIAL_FLAG_DRAIN
    if(subtype == 3)
      special_flags = SPECIAL_FLAG_DRAIN
      if((self.action != ACTION_BASH_RIGHT || self.eqright == ITEM_NONE) && self.mana > 0)
        // Cost us mana
        self.mana--


        // Throw a fireball
        x = self.x + self.frontx*3.0
        y = self.y + self.fronty*3.0
        z = self.z + 2.2
        if(self.action == ACTION_BASH_LEFT)
          x = self.x+self.sidex
          y = self.y+self.sidey
        else
          x = self.x-self.sidex
          y = self.y-self.sidey

        dex = self.dex
        if(dex > 50)
          dex = 50
        AutoAim(0.50, 0.10, self.spin, self.team, dex, 8192 + (dex*164), AUTOAIM_CRUNCH)
        vx = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_X)
        vy = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Y)
        vz = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Z)
        PFIRBALL.ShotEffect(x, y, z, vx, vy, vz, FindIndex(self), 160)
        PSPARK.Effect(x, y, z, 10, 5)


        return 0
    damage = 1
    if(self.str >= 30)
      damage = RollDice(1, ((self.str-30)>>2)+subtype+1)
    if(self.action == ACTION_BASH_LEFT)
      // Spawn a zombi punch particle
      PATTACK.Effect(self.x-self.frontx*0.5+self.sidex, self.y-self.fronty*0.5+self.sidey, self.z+2.5, self.frontx*0.4, self.fronty*0.4, 0.0, type, damage, 1, WEAPON_RADIUS_MEDIUM, 0.0, 8, special_flags)
      PlaySound("FILE:SWIPE.RAW", 225 + RollDice(1, 50), 255)
    if(self.action == ACTION_BASH_RIGHT)
      if(self.eqright == ITEM_NONE)
        // Spawn a zombi punch particle
        PATTACK.Effect(self.x-self.frontx*0.5-self.sidex, self.y-self.fronty*0.5-self.sidey, self.z+2.5, self.frontx*0.4, self.fronty*0.4, 0.0, type, damage, 1, WEAPON_RADIUS_MEDIUM, 0.0, 8, special_flags)
        PlaySound("FILE:SWIPE.RAW", 225 + RollDice(1, 50), 255)
      else
        STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype, head
  self = FindSelf()
  subtype = self.eqcol01>>6
  head = (self.eqcol01 & 8)


  ModelClear()
  ModelAssign(self+MODEL_BASE_FILE, "FILE:ZOMBANIM.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)


  if(subtype == 0)
    // Zombi
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:ZOMBBODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DMAGENTA.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:RED.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=ZOMBI.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=ZOMBI.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, YELLOW)

    if(head)
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:ZOMBHEAD.RDY")
    else
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:ZOMBDCAP.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:DMAGENTA.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:DBLUE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=ZOMBI.RGB")
    STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
    ModelAssign(self+MODEL_HEAD_COLOR, YELLOW)
  if(subtype == 1)
    // Ghoul
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:ZOMBBODY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:BLUE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:=ZOMBI.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=ZOMBI.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, YELLOW)

    if(head)
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:ZOMBHEAD.RDY")
    else
      ModelAssign(self+MODEL_HEAD_FILE, "FILE:ZOMBDCAP.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:DGREEN.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:PURPLE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:=ZOMBI.RGB")
    STANDARD.EyeSetup(self+MODEL_HEAD_TEX3, self.team)
    ModelAssign(self+MODEL_HEAD_COLOR, YELLOW)
  if(subtype == 2)
    // Mummy
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:MUMMY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:NWHITE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:BLACK.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:SHADMUMM.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=EYE1.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, DARK_RED)
  if(subtype == 3)
    // Ex Pharoh
    ModelAssign(self+MODEL_CHEST_FILE, "FILE:MUMMY.RDY")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:GYELLOW.RGB")
    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:DBLUE.RGB")
    ModelAssign(self+MODEL_CHEST_TEX2, "FILE:SHADMUMM.RGB")
    ModelAssign(self+MODEL_CHEST_TEX3, "FILE:=EYE1.RGB")
    ModelAssign(self+MODEL_CHEST_COLOR, CYAN)
  STANDARD.ItemModelSetup(MODEL_RIGHT_FILE)

//-----------------------------------------------------------------------
int GetName()
  int self, subtype
  self = FindSelf()
  subtype = self.eqcol01>>6
  StringClear(NAME_STRING)
  if(self.action != ACTION_SPECIAL_0)
    if(subtype == 0)
      StringAppend(NAME_STRING, "Zombi", 256)
    if(subtype == 1)
      StringAppend(NAME_STRING, "Ghoul", 256)
    if(subtype == 2)
      StringAppend(NAME_STRING, "Mummy", 256)
    if(subtype == 3)
      StringAppend(NAME_STRING, "Ex Pharoh", 256)
    STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  int self, target, subtype
  float x, y, distance
  self = FindSelf()
  if(self.daction == ACTION_SPECIAL_0)
    // Special code for hidden Zombi
    if(FindByIndex(AcquireTarget(ACQUIRE_SEE_ENEMY | ACQUIRE_SEE_BEHIND, NULL, 15.0)))
      ButtonPress(BUTTON_PRESS, BUTTON_RIGHT, 0)
    self.aitimer = 40 + RollDice(1, 10)
  else
    subtype = self.eqcol01>>6
    if(RollDice(1, 6) <= subtype)
      self.target = AcquireTarget(ACQUIRE_SEE_ENEMY, NULL, 20.0)
      target = FindTarget()
      if(target)
        if(LineOfSight(target.x, target.y, target.z+target.height))
          self.gotox = (target.x + self.x)*0.5
          self.gotoy = (target.y + self.y)*0.5
          x = target.x - self.x
          y = target.y - self.y
          distance = x*x + y*y

          if(distance < 100.0)
            if(GetDirection(target.x, target.y) == DIRECTION_FRONT)
              if(self.eqspec2 == ITEM_NONE)
                ButtonPress(BUTTON_PRESS, BUTTON_SPECIAL2, 0)


    STANDARD.AIUseMeat()
    STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  STANDARD.Refresh()

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype, item_common
  float difficulty
  self = FindSelf()
  subtype = self.eqcol01>>6

  self.boxsize = 1.75
  self.height = 5
  self.flags = CHAR_GRAVITY_ON | CHAR_CAN_RIDE_MOUNT
  self.sflags = SCRIPT_NO_POISON
  self.oflags = OTHER_FLAG_NO_HEARTS
  self.vspin = 256
  self.timer = 1
  self.setimer = RollDice(1, 200)*4  // Brains...


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.8)
  item_common = ITEM_NONE
  if(subtype > 1)
    item_common = ITEM_HEAL
  STANDARD.TreasureSetup(difficulty, 10, 1.0, item_common, ITEM_MEAT_ROTTEN)
  self.hitsmax = 14*difficulty
  self.hits = self.hitsmax
  self.str = (5.5*difficulty)+12
  self.dex = (6.5*difficulty)+2
  self.int = 0
  if(self.team == TEAM_GOOD)
    if(self.str < 30)
      self.str = 30
    if(self.dex < 30)
      self.dex = 30
  self.defacid = (subtype*2)+2
  self.deffire = (subtype*-2)-2
  self.defice  = 2
  self.defwoun = 50
  self.eqspec1 = ITEM_JUMP


  // Give out weapons...
  if(subtype == 1)
    self.eqright = ITEM_BROAD_AXE
  if(subtype == 2)
    self.eqright = ITEM_OBSIDIAN_BLADE
  if(subtype == 3)
    self.eqright = ITEM_GREAT_SWORD | WEAPON_VARIANT_FIRE
    self.defacid = 50
    self.defedge = 50
    self.defbash = 50



  self.eqcol01 = self.eqcol01 | 8  // Turn head on...
  STANDARD.FixStats(self)
  ModelSetup()


  // Make zombi's and ghouls disenchantable...
  if(subtype == 0 || subtype == 1)
    self.eflags = ENCHANT_FLAG_SUMMON_1


  // Special popper position for Zombi
  if(subtype == 0)
    self.action = ACTION_SPECIAL_0
    self.daction = ACTION_SPECIAL_0
    self.flags = self.flags | CHAR_NO_COLLISION
    self.alpha = 15
  else
    STANDARD.PutOnMount()



//-----------------------------------------------------------------------
