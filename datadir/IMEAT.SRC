//-----------------------------------------------------------------------
int GetName()
  int type
  type = SystemGet(SYS_ITEMINDEX, 0, 0)
  if(type == ITEM_MEAT)
    STANDARD.ItemName("MONSTER MEAT", NULL, 255, 0, 0, 0, 0)
  else
    STANDARD.ItemName("MONSTER MEAT", "ROTTEN", 255, 0, 0, 0, 0)
    

//-----------------------------------------------------------------------
// What this item does when used...
int Effect(int self)
  int index

  if(self)
    PlaySound("FILE:-EAT.RAW", 256, 255)
    if(self.food > 0)
      ExperienceFunction(FindIndex(self), EXPERIENCE_VIRTUE_FAITH, -10, FALSE)
      // Feed the character...
      self.food = self.food + 28000
      if(self.food > MAX_FOOD)
        self.food = MAX_FOOD


    // Heal undead helpers...
    if(self.defwoun > 0)
      RestoreCharacterHits(self, (self.hitsmax+1)>>1, 1)


    if(SystemGet(SYS_ITEMINDEX, 0, 0) == ITEM_MEAT_ROTTEN)
      // Rotten food causes poison...
      index = FindIndex(self)
      STANDARD.PoisonCharacter(index, index, 1800)


//-----------------------------------------------------------------------
// Called when item is first used, to start proper animation
int Event()
  int self, grip

  self = FindSelf()
  grip = SystemGet(SYS_WEAPONGRIP, 0, 0)
  if(self.virfait != 255)
    STANDARD.AutoReload(self, grip)
    Effect(self)
  else
    BuzzerSound()
  ButtonHandled()

//-----------------------------------------------------------------------
// Called when right clicked in inventory...
int DirectUsage()
  int self

  self = FindBinding()
  if(self)
    if(self.virfait != 255)
      Effect(self)
      SystemSet(SYS_KEEPITEM, 0, 0, FALSE)
    else
      BuzzerSound()

//-----------------------------------------------------------------------

